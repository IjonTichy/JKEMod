
actor PunchSide: Inventory
{
    Inventory.MaxAmount 1
    +UNDROPPABLE
}

actor " J-Fists ": Weapon
{
    +UNDROPPABLE
    +NOALERT
    +WEAPON.MELEEWEAPON
    +WEAPON.WIMPY_WEAPON

    Weapon.SelectionOrder 1000
    Weapon.Kickback 100

    Weapon.SisterWeapon " Power J-Fists "
    Inventory.PickupMessage "$JKE_PK_ZENFIST"
    Obituary "$JKE_MP_ZENFIST"

    States
    {
      Ready:
        FIST A 1 A_WeaponReady
        Loop
      Deselect:
        FIST A 0 A_Lower
        FIST A 1 A_Lower
        Loop
      Select:
        FIST A 0 A_Raise
        FIST A 1 A_Raise
        Loop
      Fire:
        FIST B 0 A_JumpIfInventory("PunchSide",1,6)
        FIST B 2
        FIST C 3 A_Punch
        FIST BA 2
        FIST A 0 A_GiveInventory("PunchSide")
        Goto Ready
        FIST D 2
        FIST E 3 A_Punch
        FIST DA 2
        FIST A 0 A_TakeInventory("PunchSide")
        Goto Ready
      AltFire:
        KICK ABC 2
        KICK D 3 A_CustomPunch(40,1,0,"ZenFistPuff")
        KICK EDCB 2
        KICK A 2 A_ReFire
        Goto Ready
    }
}

actor " Power J-Fists ": " J-Fists "
{
    +UNDROPPABLE
    +NOALERT
    +WEAPON.MELEEWEAPON
    +WEAPON.WIMPY_WEAPON
    +POWERED_UP

    Weapon.SelectionOrder 1000
    Weapon.Kickback 100

    Weapon.SisterWeapon " J-Fists "
    Obituary "$JKE_MP_ZENFIST_POWER"

    States
    {
      Ready:
        FIST A 1 A_WeaponReady
        Loop
      Deselect:
        FIST A 0 A_Lower
        FIST A 1 A_Lower
        Loop
      Select:
        FIST A 0 A_Raise
        FIST A 1 A_Raise
        Loop
      Fire:
        FIST B 0 A_JumpIfInventory("PunchSide",1,6)
        FIST B 2
        FIST C 1 A_Punch
        FIST BA 2
        FIST A 0 A_GiveInventory("PunchSide")
        Goto Ready
        FIST D 2
        FIST E 1 A_Punch
        FIST DA 2
        FIST A 0 A_TakeInventory("PunchSide")
        Goto Ready
      AltFire:
        KICK ABC 2
        KICK D 1 A_CustomPunch(65,1,0,"ZenFistPuff")
        KICK EDCB 2
        KICK A 2 A_ReFire
        Goto Ready
    }
}

actor ZenFistPuff: BulletPuff
{
    +PUFFONACTORS
    SeeSound "jkemod/zenfist/altfire"

    States
    {
      Melee:
        TNT1 A 0
        stop

      Crash:
        PUFF CD 4
        stop
    }
}
