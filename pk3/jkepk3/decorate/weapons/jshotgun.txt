actor " J-Shotgun " : Weapon
{
    Weapon.SelectionOrder 430

    Weapon.AmmoType1 "JShellCounter"
    Weapon.AmmoType2 "Shell"
    Weapon.AmmoUse1 1
    Weapon.AmmoGive1 16
    Weapon.AmmoGive2 12

    +AMMO_OPTIONAL
    +NOALERT

    Weapon.SisterWeapon " Power J-Shotgun "

    Decal BulletChip

    Inventory.PickupMessage "$JKE_PK_JSHOTGUN"
    Obituary "$JKE_MP_JSHOTGUN"

    States
    {
      Select:
        DHSG A 1 A_Raise
        Loop

      Deselect:
        DHSG A 1 A_Lower
        Loop

      Ready:
        DHSG A 1 A_WeaponReady
        DHSG A 0 A_JumpIfInventory("ReloadButton", 1, "AltFire")
        loop

      Fire:
        DHSG A 0 A_JumpIfNoAmmo("AltFire")

        DHSG A 0 A_AlertMonsters
        DHSG A 0 A_PlaySound("jkemod/jshotgun/fire", CHAN_WEAPON)
        DHSF A 0 A_Light1
        DHSF A 3 bright A_FireBullets(3, 3, 10, 7, "BulletPuff", 1)
        DHSF BA 3
        DHSG A 1 A_Light0
        DHSG BCD 3
        DHSG E 3 A_PlaySound("jkemod/jshotgun/pump")
        DHSG E 3 A_FireCustomMissile("ShellCasing", -50+random(-3, 3), 0, 5, 7)
        DHSG DCBA 3
        DHSG A 0 A_ReFire
        Goto Ready

      AltFire:
        DHSG A 0 A_JumpIfInventory("JShellCounter", 0, "NoReload")
        DHSG A 0 A_JumpIfInventory("Shell", 1, "ReloadStart")
        Goto NoReload

      ReloadStart:
        DHSG F 3
        DHSR G 8
        Goto ReloadLoop

      ReloadLoop:
        DHSR A 3
        DHSR B 2
        DHSR C 2
        DHSR D 2 A_PlaySound("jkemod/jshotgun/load", CHAN_WEAPON)
        DHSR E 1
        DHSR E 0
        DHSR E 0 A_TakeInventory("Shell", 1)
        DHSR E 0 A_GiveInventory("JShellCounter", 1)
        DHSR E 1
        DHSR F 2 A_JumpIfInventory("MainFired", 1, "ReloadEnd")
        DHSR G 3
        DHSR G 0 A_JumpIfInventory("JShellCounter", 8, "ReloadEnd")
        DHSR G 0 A_JumpIfInventory("Shell", 1, "ReloadLoop")
        Goto ReloadEnd

      ReloadEnd:
        DHSR H 5  A_PlaySound("jkemod/jshotgun/pump", CHAN_WEAPON)
        DHSR G 8
        DHSG F 3
        Goto Ready

      NoReload:
        DHSR A 0
        goto Ready

      Spawn:
        DHSP A -1
        Stop
    }
}

actor " Power J-Shotgun ": " J-Shotgun "
{
    Weapon.AmmoGive1 0
    Weapon.AmmoGive2 0

    Weapon.SisterWeapon " J-Shotgun "
    +POWERED_UP

    Decal BulletScorch

    Obituary "$JKE_MP_JSHOTGUN_POWER"

    States
    {
      Fire:
        DHSG A 0 A_JumpIfNoAmmo("AltFire")

        DHSG A 0 A_AlertMonsters
        DHSG A 0 A_PlaySound("jkemod/jshotgun/fire", CHAN_WEAPON)
        DHSF A 0 A_Light1
        DHSF A 3 bright A_FireBullets(3, 3, 10, 7, "ExplosiveBulletPuff", 1)
        DHSF BA 3
        DHSG A 1 A_Light0
        DHSG BCD 2
        DHSG E 2 A_PlaySound("jkemod/jshotgun/pump")
        DHSG E 2 A_FireCustomMissile("ShellCasing", -50+random(-3, 3), 0, 5, 7)
        DHSG DCBA 2
        DHSG A 0 A_ReFire
        Goto Ready


      AltFire:
        DHSG A 0 A_JumpIfInventory("JShellCounter", 0, "NoReload")
        DHSG A 0 A_JumpIfInventory("Shell", 2, "ReloadStart")
        Goto NoReload

      ReloadStart:
        DHSG F 2
        DHSR G 5
        Goto ReloadLoop

      ReloadLoop:
        DHSR A 2
        DHSR B 1
        DHSR C 1
        DHSR D 1 A_PlaySound("jkemod/jshotgun/load", CHAN_WEAPON)
        DHSR E 1
        DHSR E 0
        DHSR E 0 A_TakeInventory("Shell", 2)
        DHSR E 0 A_GiveInventory("JShellCounter", 1)
        DHSR E 1
        DHSR F 1 A_JumpIfInventory("MainFired", 1, "ReloadEnd")
        DHSR G 2
        DHSR G 0 A_JumpIfInventory("JShellCounter", 8, "ReloadEnd")
        DHSR G 0 A_JumpIfInventory("Shell", 2, "ReloadLoop")
        Goto ReloadEnd

      ReloadEnd:
        DHSR H 3  A_PlaySound("jkemod/jshotgun/pump", CHAN_WEAPON)
        DHSR G 5
        DHSG F 2
        Goto Ready
    }
}

actor JShellCounter: Ammo
{
    +IGNORESKILL
    Inventory.Amount 1
    Inventory.MaxAmount 8

    Ammo.BackpackAmount 0
    Ammo.BackpackMaxAmount 8
}
