
actor " S-Plasma Beam ": Weapon
{
    Weapon.SelectionOrder 500

    Weapon.AmmoType1 "Cell"
    Weapon.AmmoType2 "Cell"
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 1
    Weapon.AmmoGive1 75

    Weapon.SisterWeapon " Power S-Plasma Beam "

    Inventory.PickupMessage "$STRIFEMARINE_PK_DWEAPPLASMABEAM"
    Obituary "$STRIFEMARINE_MP_DWEAPPLASMABEAM"

    states
    {
      Deselect:
        PLSB A 1 A_Lower
        PLSB A 0 A_StopSoundEx("Weapon")
        loop

      Select:
        PLSB A 1 A_Raise
        PLSB A 0 A_StopSoundEx("Weapon")
        loop

      Ready:
        PLSB A 1 A_WeaponReady
        loop

      Fire:
        PLSB A 0 A_GunFlash
        PLSB A 0 A_PlaySound("hexenmarine/beamproj", CHAN_WEAPON)
        PLSB A 4 bright A_FireCustomMIssile("PlasmaBeamShot", 0, 1, random(-3,3), random(-3,3))
        PLSB A 0 A_ReFire
        PLSB A 0 A_PlaySound("hexenmarine/beamprojstop", CHAN_WEAPON)
        goto Ready

      AltFire:
        PLSB A 0 A_PlaySoundEx("hexenmarine/beamfire", "Weapon", 1)
        goto AltHold

      AltHold:
        PLSB A 0 bright A_GunFlash
        PLSB A 1 bright A_FireCustomMissile("PlasmaBeamBeam", 0, 1)
        PLSB AA 1 bright A_FireCustomMissile("PlasmaBeamBeam", 0, 0)
        PLSB A 1 A_ReFire
        PLSB A 0 A_StopSoundEx("Weapon")
        PLSB A 0 A_PlaySound("hexenmarine/beamprojstop", CHAN_WEAPON)
        goto Ready

      Flash:
        PLSB B 2 bright A_Light1
        PLSB C 3 bright A_Light2
        PLSB A 0 bright A_Light0
        stop

      Spawn:
        PBPK A -1
        Stop
    }
}

actor " Power S-Plasma Beam ": " S-Plasma Beam "
{
    Weapon.AmmoGive1 0
    Weapon.AmmoGive2 0

    Weapon.SisterWeapon " S-Plasma Beam "
    +POWERED_UP

    Obituary "$STRIFEMARINE_MP_DWEAPPLASMABEAM_POWER"

    states
    {
      Fire:
        PLSB A 0 A_GunFlash
        PLSB A 0 A_PlaySound("hexenmarine/beamproj", CHAN_WEAPON)
        PLSB A 3 bright A_FireCustomMIssile("PowerPlasmaBeamShot", 0, 1, random(-3,3), random(-3,3))
        PLSB A 0 A_ReFire
        PLSB A 0 A_PlaySound("hexenmarine/beamprojstop", CHAN_WEAPON)
        goto Ready

      AltFire:
        PLSB A 0 A_PlaySoundEx("hexenmarine/beamfire", "Weapon", 1)
        goto AltHold

      AltHold:
        PLSB A 0 bright A_GunFlash
        PLSB A 1 bright A_FireCustomMissile("PowerPlasmaBeamBeam", 0, 1)
        PLSB A 1 bright A_FireCustomMissile("PowerPlasmaBeamBeam", 0, 0)
        PLSB A 1 A_ReFire
        PLSB A 0 A_StopSoundEx("Weapon")
        PLSB A 0 A_PlaySound("hexenmarine/beamprojstop", CHAN_WEAPON)
        goto Ready

      Flash:
        PLSB B 1 bright A_Light1
        PLSB C 3 bright A_Light2
        PLSB A 0 bright A_Light0
        stop
    }
}

actor PlasmaBeamShot
{
    Radius 8
    Height 16
    Speed 40

    Projectile
    Damage (10*(random(20,40)/10.0))

    Scale 0.25
    RenderStyle Add

    DeathSound "hexenmarine/beamshothit"
    Decal DoomImpScorch

    Obituary "$STRIFEMARINE_MP_DWEAPPLASMABEAM_PLASMA"

    States
    {
      Spawn:
        PLSP ABC 4 bright
        loop
      Death:
        PLSP BCDE 4 bright
        stop
    }
}

actor PowerPlasmaBeamShot: PlasmaBeamShot
{
    Radius 8
    Height 16
    Speed 40

    Projectile
    Damage (20+random(0,5))

    Scale 0.35
    RenderStyle Add

    +RIPPER
    +FORCERADIUSDMG

    DeathSound "hexenmarine/beamshothit"
    Decal DoomImpScorch

    Obituary "$STRIFEMARINE_MP_DWEAPPLASMABEAM_PLASMA_POWER"

    States
    {
      Spawn:
        PLSP ABC 4 bright
        loop
      Death:
        PLSP A 0 A_Explode(32, 96, 0)
        PLSP BCDE 4 bright
        stop
    }
}

actor PlasmaBeamBeam: FastProjectile
{
    Height 1
    Radius 0.2

    Speed 512
    Damage (10)
    +PUFFONACTORS
    +ALWAYSPUFF

    RenderStyle Add
    MissileType PlasmaBeamTrail

    Decal DoomImpScorch
    Scale 0.25

    States
    {
      Spawn:
        PLSP A 1
        goto Death

      Death:
        PLSP A 0 A_Stop
        PLSP ABCDE 4 bright
        stop
    }
}


actor PowerPlasmaBeamBeam: FastProjectile
{
    Height 1
    Radius 0.2

    Speed 512
    Damage (10)

    //+PUFFONACTORS
    //+ALWAYSPUFF

    +NODAMAGETHRUST

    RenderStyle Add
    MissileType PlasmaBeamTrail

    Decal DoomImpScorch
    Scale 0.25

    States
    {
      Spawn:
        PLSP A 1
        goto Death

      Death:
        PLSP A 0 A_Stop
        PLSP A 0 A_Explode(16, 96, 0)
        PLSP ABCDE 4 bright
        stop
    }
}

ACTOR PlasmaBeamTrail
{
    +NOINTERACTION
    Scale 0.125
    RenderStyle Add
    States
    {
      Spawn:
        PLSP A 0
        PLSP A 0 A_Jump(255, "1", "2", "3", "4", "5")
      1:
        PLSP A 1
        stop
      2:
        PLSP B 1
        stop
      3:
        PLSP C 1
        stop
      4:
        PLSP D 1
        stop
      5:
        PLSP E 1
        stop
    }
}
