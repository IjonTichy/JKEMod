
actor ShotgunCounter: Ammo
{
    Inventory.Amount       1
    Inventory.MaxAmount    5

    Ammo.BackpackAmount    0
    Ammo.BackpackMaxAmount 5

    Inventory.Icon "SHELA0"
}

actor " B-Shotgun ": DoomWeapon
{
    Weapon.SelectionOrder 1300

    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 2
    Weapon.AmmoGive1  8
    Weapon.AmmoGive2  0
    Weapon.AmmoType1    "Shell"
    Weapon.AmmoType2    "Shell"

    Weapon.SlotNumber 3
    Weapon.SisterWeapon "Power Shotgun"

    Inventory.PickupMessage "$BDW_PK_SHOTGUN"
    Obituary "$BDW_MP_SHOTGUN"

    +NOAUTOFIRE
    +NOALERT

    States
    {
      Ready:
        PKSG A 1 A_WeaponReady
        Loop
      Deselect:
        PKSG A 1 A_Lower
        Loop
      Select:
        PKSG A 1 A_Raise
        Loop

      Fire:
        PKSG A 0 A_AlertMonsters
        PKSG A 0 A_FireBullets(5, 3, 10, 7, "BulletPuff")
        PKSG A 0 A_GunFlash
        PKSG A 7 A_PlaySound("betterdoomweapons/shotfire", CHAN_WEAPON)
        goto GenericPump

      AltFire:
        PKSG A 0 A_AlertMonsters
        PKSG A 0 A_RailAttack(150, 0, 1, "none", "90 90 90", 1, 0, "ShotgunSlugPuff")
        PKSG A 0 A_GunFlash
        PKSG A 11 A_PlaySound("betterdoomweapons/shotfire", CHAN_WEAPON)
        goto GenericPump

      GenericPump:
        PKSG B 3
        PKSG C 2
        PKSG DE 1
        PKSG F 3
        PKSG A 0 A_PlaySound("betterdoomweapons/shotreload", CHAN_WEAPON)
        PKSG F 2
        PKSG G 1
        PKSG H 3
        PKSG G 1
        PKSG F 3
        PKSG E 2
        PKSG DC 1
        PKSG B 3
        PKSG B 0 A_ReFire
        Goto Ready

      Flash:
        SHTF A 4 Bright A_Light1
        SHTF B 3 Bright A_Light2
        Goto LightDone

      AltFlash:
        SHTF A 2 Bright A_Light1
        SHTF B 7 Bright A_Light2
        Goto LightDone
      Spawn:
        SHOT A -1
        Stop
    }
}


actor "Power Shotgun": DoomWeapon
{
    Weapon.SelectionOrder 1300

    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 2
    Weapon.AmmoGive1  8
    Weapon.AmmoGive2  0
    Weapon.AmmoType1    "Shell"
    Weapon.AmmoType2    "Shell"

    +NOAUTOFIRE
    +NOALERT
    +POWERED_UP

    Weapon.SisterWeapon " B-Shotgun "

    Inventory.PickupMessage "$BDW_PK_SHOTGUN"
    Obituary "$BDW_MP_SHOTGUN_POWER"

    States
    {
      Ready:
        PKSG A 1 A_WeaponReady
        Loop
      Deselect:
        PKSG A 1 A_Lower
        Loop
      Select:
        PKSG A 1 A_Raise
        Loop

      Fire:
        PKSG A 0 A_AlertMonsters
        PKSG A 0 A_FireBullets(5, 3, 10, 7, "ExplosiveShotgunBulletPuff")
        PKSG A 0 A_GunFlash
        PKSG A 7 A_PlaySound("betterdoomweapons/shotfire", CHAN_WEAPON)
        goto GenericPump

      AltFire:
        PKSG A 0 A_AlertMonsters
        PKSG A 0 A_RailAttack(250, 0, 1, "none", "90 90 90", 1, 0, "ShotgunSlugPuff")
        PKSG A 0 A_GunFlash
        PKSG A 11 A_PlaySound("betterdoomweapons/shotfire", CHAN_WEAPON)
        goto GenericPump

      GenericPump:
        PKSG B 2
        PKSG CDE 1
        PKSG F 2
        PKSG A 0 A_PlaySound("betterdoomweapons/shotreload", CHAN_WEAPON)
        PKSG F 1
        PKSG G 1
        PKSG H 3
        PKSG G 1
        PKSG F 2
        PKSG EDC 1
        PKSG B 2
        PKSG B 0 A_ReFire
        Goto Ready

      Flash:
        SHTF A 4 Bright A_Light1
        SHTF B 3 Bright A_Light2
        Goto LightDone

      AltFlash:
        SHTF A 2 Bright A_Light1
        SHTF B 7 Bright A_Light2
        Goto LightDone
      Spawn:
        SHOT A -1
        Stop
    }
}


actor ShotgunSlugPuff: BulletPuff
{
    +PUFFGETSOWNER
    decal RailScorchLower
    Obituary "%o was slugged by %k's shotgun."
}

actor ExplosiveShotgunBulletPuff: BulletPuff
{
    +PUFFONACTORS
    +PUFFGETSOWNER
    Obituary "$OP_MPSHOTGUN_POWERED"
    Vspeed 0
    Decal BulletScorch
    RenderStyle Add
    Scale 0.5
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0
        MISL B 3 bright A_Explode(48,96, 0)

      Death:
        MISL C 6 bright
        MISL D 4 bright
        stop
    }
}