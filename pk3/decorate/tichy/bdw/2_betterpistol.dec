
actor " B-Pistol ": DoomWeapon
{
    Weapon.SelectionOrder 1900
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 4
    Weapon.AmmoGive 20
    Weapon.AmmoType1 "Clip"
    Weapon.AmmoType2 "Clip"

    Weapon.SisterWeapon " Power B-Pistol "

    +WEAPON.WIMPY_WEAPON
    +NOAUTOFIRE

    Decal BulletChip

    Inventory.PickupMessage "$BDW_PK_PISTOL"
    Obituary "$BDW_MP_PISTOL"
    States
    {
      Select:
        PKPI E 0 A_Raise
        PKPI E 1 A_Raise
        loop

      Deselect:
        PKPI C 0 A_Lower
        PKPI C 1 A_Lower
        loop

      Ready:
        PKPI A 1 A_WeaponReady
        loop

      Fire:
        PKPI B    0 A_GunFlash
        PKPI B    0 A_PlaySound("betterdoomweapons/pistolfire", CHAN_WEAPON)
        PKPI B    1 A_FireBullets(1.5, 1, -1, 5, "BulletPuff")
        PKPI CEED 1
        PKPI CB   1 A_WeaponReady
        goto Ready

      AltFire:
        PKPI B 0 A_GunFlash
        PKPI B 0 A_PlaySound("betterdoomweapons/pistolaltfire", CHAN_WEAPON)
        PKPI B 0 A_FireBullets(5, 3, 4, 5, "BulletPuff")
        PKPI C 1
        PKPI E 12
        PKPI D 3
        PKPI C 2
        PKPI B 1
        PKPI B 0 A_ReFire
        goto Ready

      Flash:
        PKPF A 1 Bright A_Light1
        goto LightDone

      Spawn:
        PIST A -1
        stop
    }
}


actor " B-Pistols ": DoomWeapon
{
    Weapon.SelectionOrder 1500
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 8
    Weapon.AmmoGive 20
    Weapon.AmmoType1 "Clip"
    Weapon.AmmoType2 "Clip"

    Weapon.SisterWeapon " Power B-Pistols "

    +NOAUTOFIRE

    Decal BulletChip

    Inventory.PickupMessage "$BDW_PK_PISTOLS"
    Obituary "$BDW_MP_PISTOLS"

    States
    {
      Select:
        BPKP D 0 A_Raise
        BPKP D 1 A_Raise
        loop

      Deselect:
        BPKP C 0 A_Lower
        BPKP C 1 A_Lower
        loop

      Ready:
        BPKP A 1 A_WeaponReady
        loop

      Fire:
        BPKP F    0 A_PlaySound("betterdoomweapons/pistolfire", 5)
        BPKP F    0 A_FireBullets(1.5, 1, -1, 5, "BulletPuff")
        BPKP GH 1 bright
        BPKP I 1
        BPKP I 0 A_JumpIfNoAmmo("NoFire")
        BPKP I 0 A_JumpIfInventory("MainFired", 1, "Fire2")
        goto NoFire

      NoFire:
        BPKP V 2
        BPKP WXY 1
        goto Ready

      Fire2:
        BPKP J 1
        BPKP K    0 A_PlaySound("betterdoomweapons/pistolfire", 6)
        BPKP K    0 A_FireBullets(1.5, 1, -1, 5, "BulletPuff")
        BPKP KL 1 bright
        BPKP MNOP 1
        BPKP I 0 A_JumpIfNoAmmo("NoHold")
        BPKP QR 1 A_ReFire
        goto Ready

      NoHold:
        BPKP QR 1
        goto Ready

      Hold:
        BPKP T    0 A_PlaySound("betterdoomweapons/pistolfire", 5)
        BPKP T    0 A_FireBullets(1.5, 1, -1, 5, "BulletPuff")
        BPKP TU 1 bright
        goto Fire2



      AltFire:
        BPKP F    0 A_PlaySound("betterdoomweapons/pistolaltfire", 5)
        BPKP F    0 A_PlaySound("betterdoomweapons/pistolaltfire", 6)
        BPKP F    0 A_FireBullets(6, 6, 8, 5, "BulletPuff")
        BPKP FD 1 bright
        BPKP E 14
        BPKP D 4
        BPKP C 2
        BPKP B 1
        goto Ready


      Spawn:
        PIST A -1
        stop
    }
}


actor " Power B-Pistol ": " B-Pistol "
{
    Weapon.SisterWeapon " B-Pistol "
    +POWERED_UP

    Decal BulletScorch

    Obituary "$BDW_MP_PISTOL_POWER"

    States
    {
      Fire:
        PKPI B   0 A_GunFlash
        PKPI B   0 A_PlaySound("betterdoomweapons/pistolfire", CHAN_WEAPON)
        PKPI B   1 A_FireBullets(0.5, 0.5, -1, 9, "ExplosiveBulletPuff")
        PKPI CEE 1
        PKPI DCB 1 A_WeaponReady
        goto Ready

      AltFire:
        PKPI B 0 A_GunFlash
        PKPI B 0 A_PlaySound("betterdoomweapons/pistolaltfire", CHAN_WEAPON)
        PKPI B 0 A_FireBullets(3, 3, 4, 9, "ExplosiveBulletPuff")
        PKPI C 1
        PKPI E 8
        PKPI D 3
        PKPI C 2
        PKPI B 1
        PKPI B 0 A_ReFire
        goto Ready

      Flash:
        PKPF A 1 Bright A_Light1
        goto LightDone
    }
}


actor " Power B-Pistols ": " B-Pistols "
{
    Weapon.AmmoGive 0

    Weapon.SisterWeapon " B-Pistols "
    +POWERED_UP

    Decal BulletScorch

    Obituary "$BDW_MP_PISTOLS_POWER"

    States
    {
      Fire:
        BPKP F    0 A_PlaySound("betterdoomweapons/pistolfire", 5)
        BPKP F    0 A_FireBullets(1.5, 1, -1, 5, "ExplosiveBulletPuff")
        BPKP GH 1 bright
        BPKP I 1
        BPKP I 0 A_JumpIfNoAmmo("NoFire")
        BPKP I 0 A_JumpIfInventory("MainFired", 1, "Fire2")
        goto NoFire

      NoFire:
        BPKP V 2
        BPKP WXY 1
        goto Ready

      Fire2:
        BPKP J 1
        BPKP K    0 A_PlaySound("betterdoomweapons/pistolfire", 6)
        BPKP K    0 A_FireBullets(1.5, 1, -1, 5, "ExplosiveBulletPuff")
        BPKP KL 1 bright
        BPKP MNOP 1
        BPKP I 0 A_JumpIfNoAmmo("NoHold")
        BPKP QR 1 A_ReFire
        goto Ready

      NoHold:
        BPKP QR 1
        goto Ready

      Hold:
        BPKP T    0 A_PlaySound("betterdoomweapons/pistolfire", 5)
        BPKP T    0 A_FireBullets(1.5, 1, -1, 5, "ExplosiveBulletPuff")
        BPKP TU 1 bright
        goto Fire2



      AltFire:
        BPKP F    0 A_PlaySound("betterdoomweapons/pistolaltfire", 5)
        BPKP F    0 A_PlaySound("betterdoomweapons/pistolaltfire", 6)
        BPKP F    0 A_FireBullets(6, 6, 8, 5, "ExplosiveBulletPuff")
        BPKP FD 1 bright
        BPKP E 14
        BPKP D 4
        BPKP C 2
        BPKP B 1
        goto Ready
    }
}
