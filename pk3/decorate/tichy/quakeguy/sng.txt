actor " Quake SNG ": Weapon replaces Chaingun
{
    Weapon.Kickback 100
    Weapon.SelectionOrder 4
    Weapon.AmmoUse 2
    Weapon.AmmoGive 30
    Weapon.AmmoType "QuakeNails"

    Weapon.SlotNumber 5

    +FLOATBOB

    Inventory.Icon "WSNGA0"
    Inventory.PickupMessage "You got the super nailgun"
    Obituary "%o is punctured by %k"

    States
    {
      Deselect:
        TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Lower
        SNGG A 1 A_Lower
        Loop

      Select:
        TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Raise
        SNGG A 1 A_Raise
        Loop

      Ready:
        SNGG A 0 A_TakeInventory("SuperNailgunFrame")
        SNGG A 1 A_WeaponReady
        Loop



      Fire:
        SNGG A 0 A_GunFlash
        SNGG A 0 A_PlaySound("weapons/minigun", CHAN_WEAPON)
        SNGG A 0 A_FireCustomMissile("QuakeSuperNail")

        SNGG A 0 A_JumpIfInventory("SuperNailgunFrame", 4, "FireFromA")
        SNGG A 0 A_JumpIfInventory("SuperNailgunFrame", 3, "FireFromD")
        SNGG A 0 A_JumpIfInventory("SuperNailgunFrame", 2, "FireFromC")
        SNGG A 0 A_JumpIfInventory("SuperNailgunFrame", 1, "FireFromB")
        goto FireFromA  // panic?

      FireFromA:
        SNGG A 1 bright A_Light1
        SNGG B 1 bright A_Light1
        SNGG C 1 bright A_Light0
        SNGG D 0 A_TakeInventory("SuperNailgunFrame")
        SNGG D 0 A_GiveInventory("SuperNailgunFrame", 3)
        SNGG D 0 A_Refire
        goto FireEnd

      FireFromB:
        SNGG B 1 bright A_Light1
        SNGG C 1 bright A_Light1
        SNGG D 1 bright A_Light0
        SNGG D 0 A_TakeInventory("SuperNailgunFrame")
        SNGG D 0 A_GiveInventory("SuperNailgunFrame", 4)
        SNGG D 0 A_Refire
        goto FireEnd

      FireFromC:
        SNGG C 1 bright A_Light1
        SNGG D 1 bright A_Light1
        SNGG A 1 bright A_Light0
        SNGG D 0 A_TakeInventory("SuperNailgunFrame")
        SNGG D 0 A_GiveInventory("SuperNailgunFrame", 1)
        SNGG D 0 A_Refire
        goto FireEnd

      FireFromD:
        SNGG D 1 bright A_Light1
        SNGG A 1 bright A_Light1
        SNGG B 1 bright A_Light0
        SNGG D 0 A_TakeInventory("SuperNailgunFrame")
        SNGG D 0 A_GiveInventory("SuperNailgunFrame", 2)
        SNGG D 0 A_Refire
        goto FireEnd

      FireEnd:
        SNGG A 0 A_Refire
        SNGG A 0 A_JumpIfInventory("SuperNailgunFrame", 4, "Ready")   // no need to play an end
        SNGG A 0 A_JumpIfInventory("SuperNailgunFrame", 3, "FireEndC")
        SNGG A 0 A_JumpIfInventory("SuperNailgunFrame", 2, "FireEndB")
        SNGG A 0 A_JumpIfInventory("SuperNailgunFrame", 1, "FireEndA")
        goto Ready  // more panic?

      FireEndA:
        SNGG BC 1 A_WeaponReady
        SNGG C 0 A_GiveInventory("SuperNailgunFrame", 1)
        SNGG CD 1 A_WeaponReady
        SNGG D 0 A_TakeInventory("SuperNailgunFrame")
        SNGG DD 1 A_WeaponReady
        goto Ready

      FireEndB:
        SNGG CD 1 A_WeaponReady
        SNGG D 0 A_TakeInventory("SuperNailgunFrame")
        SNGG D 1 A_WeaponReady
        goto Ready

      FireEndC:
        SNGG D 1 A_WeaponReady
        SNGG D 0 A_TakeInventory("SuperNailgunFrame")
        goto Ready

      Spawn:
        WSNG A -1 bright
        Stop
    }
}

actor QuakeSNGReplace: RandomSpawner replaces Minigun
{
    dropitem " Quake SNG "
}

actor QuakeSuperNail: QuakeNail
{
    Damage (20)
}

actor SuperNailgunFrame: Inventory
{
    inventory.maxamount 4
    +UNDROPPABLE
}
