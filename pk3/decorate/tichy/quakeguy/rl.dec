actor " Quake RL ": Weapon
{
    Weapon.SelectionOrder 2
    Weapon.SlotNumber 7

    Weapon.AmmoUse 1
    Weapon.AmmoGive 5
    Weapon.AmmoType "QuakeExplosive"

    +FLOATBOB

    Inventory.Icon "W_RLA0"
    Inventory.PickupMessage "You got the rocket launcher"

    States
    {
      Deselect:
        TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Lower
        MISG A 1 A_Lower
        Loop

      Select:
        TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Raise
        MISG A 1 A_Raise
        Loop

      Ready:
        MISG A 1 A_WeaponReady
        Loop

      Fire:
        MISG B 0 A_GunFlash
        MISG B 0 A_FireCustomMissile("QuakeRocket")

        MISG A 1 offset(0, 34)
        MISG B 1 offset(0, 30)
        MISG B 2 offset(0, 32)
        MISG B 15 offset(0, 34)
        MISG B 2 offset(0, 33)
        MISG B 2 offset(0, 31)
        MISG B 2 offset(0, 28)
        MISG A 2 offset(0, 37)
        MISG A 1 offset(0, 34)
        MISG B 0 A_ReFire
        Goto Ready

      Flash:
        MISF A 3 Bright A_Light1
        MISF B 4 Bright
        MISF CD 4 Bright A_Light2
        Goto LightDone
      Spawn:
        W_RL A -1 bright
        Stop
    }
}

actor QuakeRLReplace: RandomSpawner replaces GrenadeLauncher
{
    dropitem " Quake RL "
}


actor QuakeRocket
{
    Radius 4
    Height 4
    Speed 50

    Projectile

    +RANDOMIZE
    +DEHEXPLOSION
    +ROCKETTRAIL
    +SPAWNSOUNDSOURCE

    SeeSound "weapons/rocklf"
    DeathSound "weapons/rocklx"

    Obituary "%o rides %k's rocket"

    States
    {
      Spawn:
        MISL A -1 bright
        loop

      Death:
        MISL B 0 A_Explode(180, 160, 0)
        MISL B 0 A_Explode(64,  160)
        MISL B 8 bright
        MISL C 6 bright
        MISL D 4 bright
        stop
    }
}
