actor " Quake GL ": Weapon replaces PlasmaRifle
{
    Weapon.SelectionOrder 2
    Weapon.SlotNumber 6

    Weapon.AmmoUse 1
    Weapon.AmmoGive 5
    Weapon.AmmoType "QuakeExplosive"

    +FLOATBOB

    Inventory.Icon "W_GLA0"
    Inventory.PickupMessage "You got the grenade launcher"

    States
    {
      Deselect:
        TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Lower
        GRLG A 1 A_Lower
        Loop

      Select:
        TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Raise
        GRLG A 1 A_Raise
        Loop

      Ready:
        GRLG A 1 A_WeaponReady
        Loop

      Fire:
        GRLG B 0 A_FireCustomMissile("QuakeGrenade")
        GRLG A 1 offset(0, 34)

        GRLG B 0 A_GunFlash
        GRLG B 1 offset(0, 30)
        GRLG B 1 offset(0, 32)
        GRLG B 10 offset(0, 34)
        GRLG B 2 offset(0, 33)
        GRLG B 2 offset(0, 31)
        GRLG B 2 offset(0, 28)
        GRLG A 2 offset(0, 37)
        GRLG A 1 offset(0, 34)
        GRLG B 0 A_ReFire
        Goto Ready

      Flash:
        GRLF A 3 bright A_Light1
        GRLF B 4 bright
        GRLF C 4 bright A_Light2
        GRLF D 4 bright A_Light2
        Goto LightDone

      Spawn:
        W_GL A -1 bright
        Stop
    }
}

actor QuakeGLReplace: RandomSpawner replaces Railgun
{
    dropitem " Quake GL "
}

actor QuakeGrenade
{
    Radius 4
    Height 4
    Speed 40

    Projectile
    -NOGRAVITY

    BounceFactor 0.3

    +HEXENBOUNCE
    +RANDOMIZE
    +DEHEXPLOSION
    +GRENADETRAIL
    +USESTBOUNCESOUND

    ReactionTime 90

    SeeSound "weapons/grenlf"
    DeathSound "weapons/grenlx"

    Obituary "%o eats %k's pineapple"

    States
    {
      Spawn:
        SGRN A 1 A_JumpIf(ACS_ExecuteWithResult(583, momx, momy, momz) < 1, "StopSpawnStart")
        SGRN A 0 A_Countdown
        loop

      StopSpawnStart:
        goto StopSpawn

      StopSpawn:
        SGRN A 1 A_Stop
        SGRN A 0 A_Countdown
        loop

      Death:
        MISL A 0 A_Stop
        MISL A 0 A_PlayerScream
        MISL B 0 Bright A_NoGravity
        MISL B 0 A_Explode(112, 128, 0)
        MISL B 0 A_Explode(64, 128)
        MISL B 8 bright
        MISL C 6 bright
        MISL D 4 bright
        stop
    }

}
