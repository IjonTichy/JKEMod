actor " Quake SG ": Weapon replaces Pistol
{
    Weapon.Kickback 100
    Weapon.SelectionOrder 7
    Weapon.AmmoUse 1
    Weapon.AmmoGive 5
    Weapon.AmmoType "QuakeShell"

    Weapon.SlotNumber 2

    +NOAUTOAIM
    +FLOATBOB

    Inventory.Icon "W_SGA0"
    Inventory.PickupMessage "You got the shotgun"
    Obituary "%o chews on %k's boomstick"

    States
    {
      Deselect:
        TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Lower
        SHTG A 1 A_Lower
        Loop

      Select:
        TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Raise
        SHTG A 1 A_Raise
        Loop

      Ready:
        SHTG A 1 A_WeaponReady
        Loop

      Fire:
        SHTG A 0 A_FireBullets(1.5, 1.5, 10, 2, "BulletPuff")
        SHTG A 0 A_PlaySound("weapons/shotgf", CHAN_WEAPON)
        SHTG A 0 A_GunFlash
        SHTG A 2 offset(0, 37)
        SHTG A 2 offset(0, 39)
        SHTG A 4 offset(0, 40)
        SHTG A 2 offset(0, 39)
        SHTG A 2 offset(0, 38)
        SHTG A 2 offset(0, 37)
        SHTG A 2 offset(0, 35)
        SHTG A 1 offset(0, 33)
        SHTG A 0 A_ReFire
        Goto Ready

      Flash:
        SHTF A 0 A_Light1
        SHTF A 2 bright offset(0, 38)
        SHTF A 2 bright offset(0, 39)
        SHTF B 0 A_Light2
        SHTF B 2 bright offset(0, 40)
        SHTF B 1 bright offset(0, 39)
        Goto LightDone

      Spawn:
        W_SG A -1 bright
        Stop
    }
}
