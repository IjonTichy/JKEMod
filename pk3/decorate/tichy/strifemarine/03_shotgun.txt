actor DWeapShotgun: Weapon
{
    Weapon.SelectionOrder 1500
    Weapon.SlotNumber 3

    Weapon.AmmoType1 "Shell"
    Weapon.AmmoType2 "Shell"
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 1
    Weapon.AmmoGive 8

    Inventory.PickupMessage "$STRIFEMARINE_PK_DWEAPSHOTGUN"
    Obituary "$STRIFEMARINE_MP_DWEAPSHOTGUN"

    Decal "BulletChip"
    States
    {
      Ready:
        HMSG A 1 A_WeaponReady
        Loop
      Deselect:
        HMSG A 1 A_Lower
        Loop
      Select:
        HMSG A 1 A_Raise
        Loop


      Fire:
        HMSG B 0 A_CheckReload
        HMSG B 0 A_JumpIfInventory("ShellsPumped", 1, "Fire2")
        goto FirePump

      FirePump:
        HMSG F 0 A_GiveInventory("ShellsPumped")
        HMSG FGH 1
        HMSG I 2 A_PlaySoundEx("hexenmarine/shotreload", "SoundSlot5")
        HMSG JKLKJI 2
        HMSG HGF 1
        goto Fire2

      Fire2:
        HMSG A 0 A_TakeInventory("ShellsPumped")
        HMSG A 0 A_AlertMonsters
        HMSG A 0 A_PlaySound("hexenmarine/shotfire", CHAN_WEAPON)
        HMSG A 0 A_FireBullets(2, 2, 10, 6, "HBulletPuff")
        HMSG D 1  bright
        HMSG ED 2 bright
        HMSG CB 1 bright
        HMSG A 0 A_CheckReload
        goto FirePump2

      FirePump2:
        HMSG F 0 A_GiveInventory("ShellsPumped")
        HMSG FGH 1
        HMSG I 2 A_PlaySoundEx("hexenmarine/shotreload", "SoundSlot5")
        HMSG JKLKJ 2
        HMSG K 0 A_JumpIfInventory("AltFired", 1, "AltHold")
        HMSG I 2
        HMSG HGF 1
        HMSG A 0 A_ReFire
        goto Ready


      AltFire:
        HMSG B 0 A_CheckReload
        HMSG B 0 A_JumpIfInventory("Shell", 2, 1)
        goto Fire
        HMSG B 0 A_JumpIfInventory("ShellsPumped", 2, "AltFire2")
        HMSG B 0 A_JumpIfInventory("ShellsPumped", 1, "AltFire1Pump")
        goto AltFire2Pump

      AltFire1Pump:
        HMSG F 0 A_GiveInventory("ShellsPumped")
        HMSG FGH 1
        HMSG I 2 A_PlaySoundEx("hexenmarine/shotreload", "SoundSlot5")
        HMSG JKLKJI 2
        HMSG HGF 1
        goto AltFire2

      AltFire2Pump:
        HMSG F 0 A_GiveInventory("ShellsPumped", 2)
        HMSG FGH 1
        HMSG I 2 A_PlaySoundEx("hexenmarine/shotreload", "SoundSlot5")
        HMSG JKLKJ 2
        HMSG I 0 A_PlaySoundEx("hexenmarine/shotreload", "SoundSlot5")
        HMSG JKLKJI 2
        HMSG HGF 1
        goto AltFire2

      AltFire2:
        HMSG A 0 A_TakeInventory("ShellsPumped")
        HMSG A 0 A_AlertMonsters
        HMSG A 0 A_PlaySound("hexenmarine/shotdoublefire", CHAN_WEAPON)
        HMSG AA 0 A_FireBullets(2, 2, 10, 6, "HBulletPuff")
        HMSG D 1  bright
        HMSG ED 2 bright
        HMSG CB 1 bright
        HMSG A 0 A_CheckReload
        goto AltFirePump2

      AltFirePump2:
        HMSG A 3
        HMSG F 0 A_GiveInventory("ShellsPumped")
        HMSG FGH 1
        HMSG I 2 A_PlaySoundEx("hexenmarine/shotreload", "SoundSlot5")
        HMSG JKLKJ 2
        HMSG I 0 A_JumpIfInventory("AltFired", 1, "AltHold")
        HMSG I 2
        HMSG HGF 1
        HMSG A 0
        goto Ready

      AltHold:
        HMSG I 0 A_JumpIfInventory("Shell", 2, "AltHoldPump")
        HMSG I 0 A_CheckReload // just in case
        HMSG I 2
        HMSG HGF 1
        HMSG A 0
        goto Ready

      AltHoldPump:
        HMSG I 0 A_PlaySoundEx("hexenmarine/shotreload", "SoundSlot5")
        HMSG JKLKJI 2
        HMSG HGF 1
        goto AltFire2

      Flash:
        TNT1 A 4 Bright A_Light1
        TNT1 A 3 Bright A_Light2
        Goto LightDone
      Spawn:
        HMSG Z -1
        Stop
    }
}


actor ShellsPumped: Inventory
{
    inventory.maxAmount 2
    +UNDROPPABLE
}
