
actor " S-Cluster Bomber ": Weapon
{
    Weapon.SelectionOrder 800

    Weapon.AmmoType1 "RocketAmmo"
    Weapon.AmmoType2 "RocketAmmo"
    Weapon.AmmoUse1 2
    Weapon.AmmoUse2 4
    Weapon.AmmoGive 8

    +NOAUTOFIRE
    +NOAUTOAIM

    Weapon.SisterWeapon " Power S-Cluster Bomber "

    Inventory.PickupMessage "$STRIFEMARINE_PK_DWEAPCLUSTERBOMBER"
    Obituary "$STRIFEMARINE_MP_DWEAPCLUSTERBOMBER"

    States
    {
      Ready:
        GRSP B 1 A_WeaponReady
        Loop
      Deselect:
        GRSP B 0 A_StopSound(CHAN_WEAPON)
        GRSP B 1 A_Lower
        Loop
      Select:
        GRSP B 1 A_Raise
        Loop
      Fire:
        GRSP C 1 Bright
        GRSP D 0 Bright A_PlaySound("hexenmarine/gspammerfire", CHAN_WEAPON)
        GRSP D 0 Bright A_FireCustomMissile("ClusterBomberGrenade")
        GRSP D 2 Bright
        GRSP E 3 Bright
        GRSP F 4 Bright
        GRSP B 0 A_PlaySound("hexenmarine/gspammerreload", CHAN_WEAPON)
        GRSP B 25
        GRSP B 0 A_StopSound(CHAN_WEAPON)
        GRSP A 0 A_ReFire
        Goto Ready
      AltFire:
        GRSP C 2 Bright A_GunFlash
        GRSP D 0 Bright A_PlaySound("hexenmarine/gspammerfire", CHAN_WEAPON)
        GRSP D 0 Bright A_FireCustomMissile("TinyGrenade", random(-6,6)/2.0, 1, 0, 0, 0, random(-6,6)/2.0)
        GRSP DDDDDDDDD 0 Bright A_FireCustomMissile("TinyGrenade", random(-6,6)/2.0, 0, 0, 0, 0, random(-6,6)/2.0)
        GRSP D 2 Bright
        GRSP E 3 Bright
        GRSP F 4 Bright
        GRSP B 0 A_PlaySound("hexenmarine/gspammerreload", CHAN_WEAPON)
        GRSP B 50
        Goto Ready
      Spawn:
        GRSP Z -1
        Stop
    }
}


actor " Power S-Cluster Bomber ": Weapon
{
    Weapon.AmmoType1 "RocketAmmo"
    Weapon.AmmoType2 "RocketAmmo"
    Weapon.AmmoUse1 2
    Weapon.AmmoUse2 8

    +NOAUTOFIRE
    +NOAUTOAIM

    Weapon.SisterWeapon " S-Cluster Bomber "
    +POWERED_UP

    Inventory.PickupMessage "$STRIFEMARINE_PK_DWEAPCLUSTERBOMBER"
    Obituary "$STRIFEMARINE_MP_DWEAPCLUSTERBOMBER_POWER"

    States
    {
      Ready:
        GRSP B 1 A_WeaponReady
        Loop
      Deselect:
        GRSP B 0 A_StopSound(CHAN_WEAPON)
        GRSP B 1 A_Lower
        Loop
      Select:
        GRSP B 1 A_Raise
        Loop
      Fire:
        GRSP C 1 Bright
        GRSP D 0 Bright A_PlaySound("hexenmarine/gspammerfire", CHAN_WEAPON)
        GRSP D 0 Bright A_FireCustomMissile("PowerClusterBomberGrenade")
        GRSP D 2 Bright
        GRSP E 3 Bright
        GRSP F 4 Bright
        GRSP A 0 A_ReFire
        Goto Ready
      AltFire:
        GRSP C 2 Bright A_GunFlash
        GRSP D 0 Bright A_PlaySound("hexenmarine/gspammerfire", CHAN_WEAPON)
        GRSP D 0 Bright A_FireCustomMissile("PowerClusterBomberGrenade2", random(-6,6)/2.0, 1, 0, 0, 0, random(-6,6)/2.0)
        GRSP DDD 0 Bright A_FireCustomMissile("PowerClusterBomberGrenade2", random(-6,6)/2.0, 0, 0, 0, 0, random(-6,6)/2.0)
        GRSP D 2 Bright
        GRSP E 3 Bright
        GRSP F 4 Bright
        GRSP B 0 A_PlaySound("hexenmarine/gspammerreload", CHAN_WEAPON)
        GRSP B 40
        Goto Ready
      Spawn:
        GRSP Z -1
        Stop
    }
}

Actor TinyGrenade
{
    Radius 6
    Height 12
    Speed 40

    Projectile
    Damage (15*(random(10,30)/10.0))

    -NOGRAVITY
    Gravity 0.3

    +DEHEXPLOSION

    Scale 0.7

    DeathSound "hexenmarine/gspammerexplode"
    Decal RevenantScorch

    States
    {
      Spawn:
        GRPP A 0
        GRPP A 0 ThrustThingZ(0, 8, 0, 1)
        GRPP A -1 Bright
        stop
      Death:
        MISL B 0 Bright A_ChangeFlag("NoGravity", 1)
        MISL B 6 Bright A_Explode(64, 64)
        MISL CD 6 Bright
        Stop
    }
}

Actor PowerTinyGrenade: TinyGrenade
{
    +DOOMBOUNCE
    +FORCERADIUSDMG

    States
    {
      Death:
        MISL B 0 Bright A_ChangeFlag("NoGravity", 1)
        MISL B 6 Bright A_Explode(64, 64, 0)
        MISL CD 6 Bright
        Stop
    }
}

actor ClusterBomberGrenade
{
    Radius 6
    Height 12
    Speed 40

    Projectile
    Damage (15*(random(10,30)/10.0))

    -NOGRAVITY
    Gravity 0.3

    +DEHEXPLOSION

    Scale 0.5

    DeathSound "hexenmarine/gspammerexplode"
    Decal RevenantScorch

    States
    {
      Spawn:
        GRSR A 0
        GRSR A 0 ThrustThingZ(0, 8, 0, 1)
        // fallthrough
      Spawn2:
        GRSR AB 4
        loop

      Death:
        MISL B 0 Bright A_ChangeFlag("NoGravity", 1)
        MISL B 0 A_Stop
        MISL B 0 A_Explode(32, 64)
        MISL B 0 Bright A_SpawnItemEx("TinyGrenade", 0, 0, 0, 20, 0, 2, -22.5)
        MISL B 0 Bright A_SpawnItemEx("TinyGrenade", 0, 0, 0, 20, 0, 2, -7.5)
        MISL B 0 Bright A_SpawnItemEx("TinyGrenade", 0, 0, 0, 20, 0, 2, 7.5)
        MISL B 6 Bright A_SpawnItemEx("TinyGrenade", 0, 0, 0, 20, 0, 2, 22.5)
        MISL CD 6 Bright
        Stop
    }
}

actor PowerClusterBomberGrenade: ClusterBomberGrenade
{
    States
    {
      Spawn:
        GRSR A 0
        GRSR A 0 ThrustThingZ(0, 8, 0, 1)
        // fallthrough
      Spawn2:
        GRSR AB 4
        loop

      Death:
        MISL B 0 Bright A_ChangeFlag("NoGravity", 1)
        MISL B 0 A_Stop
        MISL B 0 A_Explode(96, 96, 0)
        MISL B 0 Bright A_SpawnItemEx("PowerTinyGrenade", 0, 0, 0, 20, 0, 2, -15)
        MISL B 0 Bright A_SpawnItemEx("PowerTinyGrenade", 0, 0, 0, 20, 0, 2, 0)
        MISL B 6 Bright A_SpawnItemEx("PowerTinyGrenade", 0, 0, 0, 20, 0, 2, 15)
        MISL CD 6 Bright
        Stop
    }
}

actor PowerClusterBomberGrenade2: ClusterBomberGrenade
{
    States
    {
      Spawn:
        GRSR A 0
        GRSR A 0 ThrustThingZ(0, 8, 0, 1)
        // fallthrough
      Spawn2:
        GRSR AB 4
        loop

      Death:
        MISL B 0 Bright A_ChangeFlag("NoGravity", 1)
        MISL B 0 A_Stop
        MISL B 0 A_Explode(96, 96, 0)
        MISL B 0 Bright A_SpawnItemEx("PowerTinyGrenade", 0, 0, 0, 20, 0, 2, -22.5)
        MISL B 0 Bright A_SpawnItemEx("PowerTinyGrenade", 0, 0, 0, 20, 0, 2, -7.5)
        MISL B 0 Bright A_SpawnItemEx("PowerTinyGrenade", 0, 0, 0, 20, 0, 2, 7.5)
        MISL B 6 Bright A_SpawnItemEx("PowerTinyGrenade", 0, 0, 0, 20, 0, 2, 22.5)
        MISL CD 6 Bright
        Stop
    }
}
