
actor " Powerful " : Weapon
{
    Game Doom
    Inventory.PickupMessage "$JKE_PK_POWERFUL"
    weapon.AmmoGive1 10
    weapon.AmmoType1 "GravityAmmo"
    weapon.AmmoUse1 1
    weapon.selectionorder 390
    Obituary "$JKE_MP_POWERFUL"
    states
    {
      Select:
        PWFL A 0 A_Raise
        PWFL A 1 A_Raise
        Loop
      Deselect:
        RYGR A 0 A_Lower
        PWFL A 1 A_Lower
        Loop

      Ready:
        PWFL A 1 A_WeaponReady
        Loop
      Fire:
        PWFL A 0 A_Recoil(50)
        PWFL A 0 A_Light2
        PWFL A 0 bright A_PlaySound("jkemod/powerful/fire", CHAN_WEAPON)
        PWFL B 8 bright A_FireCustomMissile("PowerfulShot", 0, 1, 6, 0)
        PWFL A 0 A_Light0
        PWFL A 97
        PWFL A 0 A_PlaySound("jkemod/assorted/click", CHAN_WEAPON)
        Goto Ready
      Spawn:
        PWFL Z -1
        stop
    }
}

actor PowerfulShot
{

    Radius 4
    Height 8
    Speed 100

    Projectile
    Damage 1024

    +RIPPER
    +FOILINVUL       // oh _christ_
    +PIERCEARMOR

    +FORCERADIUSDMG
    +SKYEXPLODE

    +BLOODSPLATTER
    +EXTREMEDEATH
    +DEHEXPLOSION

    Decal PowerfulScorch

    States
    {
      Spawn:
        DART A 0
        DART AAAA 1 bright A_Explode(512, 192, 0)
        DART A 0 A_SpawnItemEx("PowerfulTrail", 0, 0, 0, 0, 0, 0, 0, 128, 0)
        loop
      Death:
        NKE1 A 0 A_Explode(4096, 1024)
        NKE1 A 0 A_PlaySound("jkemod/powerful/explode")
        NKE1 A 4 bright Radius_Quake(3, 35, 0, 12, 0)
        NKE1 BCDEFGHIJKLMNOPQRSTU 4 bright
        stop
    }
}

actor PowerfulTrail
{
    Projectile
    +EXTREMEDEATH
    +NOINTERACTION
    +RIPPER
    +BLOODSPLATTER
    +FOILINVUL
    +PIERCEARMOR
    +FORCERADIUSDMG
    Height 80
    Radius 80
    Damage 500
    Health 0
    Speed 0
    Mass 0x7FFFFFFFFFFFFFFF  // max mass - 9223372036854775807
    Translation "112:127=80:95"
    RenderStyle Add
    States
    {
      Spawn:
        PR2E ABCDEFGHIJKLMNOP 2 bright A_Explode(512, 256, 0)
        Stop
    }
}
