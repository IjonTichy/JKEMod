actor " Protoss-Ploder ": Weapon
{
    Weapon.AmmoType1 "Cell"
    Weapon.AmmoUse1 40
    Weapon.AmmoGive1 100

    Weapon.SelectionOrder 1000

    Inventory.PickupMessage "$JKE_PK_PROTOSSPLODER"

    States
    {
      Select:
        ISLG A 1 A_Raise
        loop

      Deselect:
        ISLG A 1 A_Lower
        loop

      Ready:
        ISLG A 1 A_WeaponReady
        Loop

      Fire:
        ISLG A 0 A_PlaySound("jkemod/protossploder/fire", CHAN_WEAPON)
        ISLG B 3 A_FireCustomMissile("ProtossShot")
        ISLG CDB 3
        ISLG A 30
        goto Ready

      Spawn:
        INLA A -1
        stop
    }
}

actor ProtossTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add

    States
    {
      Spawn:
        PRTR ABCD 5
        Stop
    }
}

actor ProtossShot
{
    Radius 6
    Height 8
    Speed 40

    Projectile
    Damage (750 + random(0, 250))

    +EXTREMEDEATH
    +FORCERADIUSDMG

    RenderStyle Add

    DeathSound "jkemod/protossploder/explode"

    Obituary "$JKE_MP_PROTOSSPLODER"

    States
    {
      Spawn:
        TNT1 A 2 A_SpawnItemEx("ProtossTrail")
        loop

      Death:
        PRSH A 0 A_Explode(256, 256)
        PRSH ABCDEFGHIJKLM 3 bright
        stop
    }
}
