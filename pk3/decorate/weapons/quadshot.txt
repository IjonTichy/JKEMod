actor " L-Quadshot ": Weapon
{
    Weapon.AmmoType1 "QuadShotCounter"
    Weapon.AmmoType2 "Shell"
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 4
    Weapon.AmmoGive1 1
    Weapon.AmmoGive2 16

    Weapon.SelectionOrder 1000

    +AMMO_OPTIONAL
    +ALT_AMMO_OPTIONAL
    +NOALERT

    Weapon.SisterWeapon " Power L-Quadshot "

    Decal BulletChip

    Inventory.PickupMessage "$JKE_PK_QUADSHOT"
    Obituary "$JKE_MP_QUADSHOT"

    States
    {
      Select:
        QSHT A 1 A_Raise
        loop
      Deselect:
        QSHT A 1 A_Lower
        loop

      Ready:
        QSHT A 1 A_WeaponReady
        GSHT A 0 A_JumpIfInventory("ReloadButton", 1, "AltFire")
        loop

      Fire:
        GSHT A 0 A_JumpIfNoAmmo("AltFire")

        QSHT A 0 A_AlertMonsters
        QSHT A 0 A_PlaySound("jkemod/quadshot/fire", CHAN_WEAPON)
        QSHT A 0 A_FireBullets(5, 5, 40, 7, "Bulletpuff", 1)
        QSHT B 1 bright offset(0, 40) A_Light2
        QSHT C 5 bright offset(0, 44)
        QSHT B 2 bright A_Light1
        QSHT B 1 bright offset(0, 42)
        QSHT A 1
        QSHT A 1 offset(0, 40)
        QSHT A 1 offset(0, 38)
        QSHT A 1 offset(0, 36)
        QSHT A 1 offset(0, 34)
        QSHT A 0 A_Light0
        QSHT A 24 offset(0, 32)
        goto Ready

      AltFire:
        QSHT A 0 A_JumpIfInventory("QuadShotCounter", 0, "NoLoad")
        QSHT A 0 A_JumpIfInventory("Shell", 4, 1)
        goto NoLoad

        QSHT A 0 A_PlaySound("jkemod/quadshot/load", CHAN_WEAPON)
        QSHT DE 4 A_WeaponReady(4)
        QSHT A 0 A_GiveInventory("QuadShotCounter", 1)
        QSHT A 0 A_TakeInventory("Shell", 4)
        QSHT AAAAAAAA 1 A_WeaponReady(12)
        goto Ready

      NoLoad:
        QSHT A 1
        QSHT A 1 A_WeaponReady
        goto Ready

      Spawn:
        QSHT Z -1
        stop
    }
}

actor " Power L-Quadshot ": " L-Quadshot "
{
    +AMMO_OPTIONAL
    +ALT_AMMO_OPTIONAL
    +NOALERT

    Weapon.SisterWeapon " L-Quadshot "
    +POWERED_UP

    Obituary "$JKE_MP_QUADSHOT_POWER"

    States
    {
      Fire:
        GSHT A 0 A_JumpIfNoAmmo("AltFire")

        QSHT A 0 A_AlertMonsters
        QSHT A 0 A_PlaySound("jkemod/quadshot/fire", CHAN_WEAPON)
        QSHT AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_FireCustomMissile("QuadshotFire", random(-30, 30)/10.0, 0, 0, 0, 0, random(-30, 30)/10.0)
        QSHT A 0 A_FireCustomMissile("QuadshotFire", random(-30, 30)/10.0, 1, 0, 0, 0, random(-30, 30)/10.0)
        QSHT B 1 bright offset(0, 40) A_Light2
        QSHT C 5 bright offset(0, 44)
        QSHT B 2 bright A_Light1
        QSHT B 1 bright offset(0, 42)
        QSHT A 1
        QSHT A 1 offset(0, 40)
        QSHT A 1 offset(0, 38)
        QSHT A 1 offset(0, 36)
        QSHT A 1 offset(0, 34)
        QSHT A 0 A_Light0
        QSHT A 24 offset(0, 32)
        goto Ready
    }
}


actor QuadshotFire
{
    Radius 14
    Height 8
    Speed 40

    Projectile
    Damage (2)
    +RIPPER
    +NODAMAGETHRUST
    +FORCERADIUSDMG

    RenderStyle Add
    Scale 0.3

    Decal BulletScorch

    SeeSound "jkemod/jflamethrower/flame"

    Obituary "$JKE_MP_QUADSHOT_POWER"

    States
    {
      Spawn:
        SOD0 E 0 bright
        SOD0 E 0 bright A_Explode(12, 64, 0)
        SOD0 E 0 bright ACS_ExecuteAlways(585, 0, 1, random(0, 50))
        SOD0 EF 2 A_Explode(8, 64, 0)
        TNT1 A 0 A_SpawnItemEx("QuadshotFlame", 0,0,0, random(0,4), 0, random(0,3), random(0,359), 0, 224)
        SOD0 GH 2 A_Explode(8, 64, 0)
        TNT1 A 0 A_SpawnItemEx("QuadshotFlame", 0,0,0, random(0,4), 0, random(0,3), random(0,359), 0, 224)
        SOD0 IJ 2 A_Explode(8, 64, 0)
        TNT1 A 0 A_SpawnItemEx("QuadshotFlame", 0,0,0, random(0,4), 0, random(0,3), random(0,359), 0, 224)
        SOD0 KL 2 A_Explode(8, 64, 0)
        TNT1 A 0 A_SpawnItemEx("QuadshotFlame", 0,0,0, random(0,4), 0, random(0,3), random(0,359), 0, 224)
        SOD0 MN 2 A_Explode(8, 64, 0)
        TNT1 A 0 A_SpawnItemEx("QuadshotFlame", 0,0,0, random(0,4), 0, random(0,3), random(0,359), 0, 224)
        SOD0 O 2 A_Explode(8, 64, 0)
        goto Death

      Death:
        TNT1 A 0 A_SpawnItemEx("QuadshotFlame", 0,0,0, random(0,4), 0, random(0,3), random(0,359), 0, 224)
        stop
    }
}

actor QuadshotFlame
{
    Speed 5

    Projectile
    -NOGRAVITY
    +NOCLIP
    +RIPPER
    +NOEXPLODEFLOOR
    +NODAMAGETHRUST
    +FORCERADIUSDMG
    +SLIDESONWALLS
    +PUFFGETSOWNER

    RenderStyle Add
    Scale 0.4

    Obituary "$JKE_MP_QUADSHOT_POWER"

    States
    {
      Spawn:
        FLME AB 2 bright A_Explode(8, 80, 0)
        FLME A 0 A_ChangeFlag("NOCLIP", 0)
        FLME CDEFGHIJKLMNABCDEFGHIJKLMN 2 bright A_Explode(8, 80, 0)
        goto XDeath

      Crash:
      Death:
        FLME A 0 A_Stop
        goto Spawn

      XDeath:
        FLME A 0 A_Jump(128, "Spawn")
        FLME O 2 bright A_Explode(6, 80, 0)
        FLME P 2 bright A_Explode(4, 80, 0)
        FLME Q 2 bright A_Explode(2, 80, 0)
        stop
    }
}

actor QuadShotCounter: Ammo
{
    Inventory.MaxAmount 4
    +IGNORESKILL
}
