
actor " L-Quadshot ": Weapon
{
    Weapon.AmmoType1 "QuadShotCounter"
    Weapon.AmmoType2 "Shell"
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 4
    Weapon.AmmoGive1 1
    Weapon.AmmoGive2 16

    Weapon.SelectionOrder 1000

    +AMMO_OPTIONAL
    +ALT_AMMO_OPTIONAL

    Inventory.PickupMessage "$JKE_PK_QUADSHOT"
    Obituary "$JKE_MP_QUADSHOT"

    States
    {
      Select:
        QSHT A 1 A_Raise
        loop
      Deselect:
        QSHT A 1 A_Lower
        loop

      Ready:
        QSHT A 1 A_WeaponReady
        loop

      Fire:
        GSHT A 0 A_JumpIfNoAmmo("AltFire")

        QSHT A 0 A_PlaySound("jkemod/quadshot/fire", CHAN_WEAPON)
        QSHT A 0 A_FireBullets(5, 5, 40, 7, "Bulletpuff", 1)
        QSHT B 1 bright offset(0, 40) A_Light2
        QSHT C 5 bright offset(0, 44)
        QSHT B 2 bright A_Light1
        QSHT B 1 bright offset(0, 42)
        QSHT A 1
        QSHT A 1 offset(0, 40)
        QSHT A 1 offset(0, 38)
        QSHT A 1 offset(0, 36)
        QSHT A 1 offset(0, 34)
        QSHT A 0 A_Light0
        QSHT A 24 offset(0, 32)
        goto Ready

      AltFire:
        QSHT A 0 A_JumpIfInventory("QuadShotCounter", 0, "NoLoad")
        QSHT A 0 A_JumpIfInventory("Shell", 4, 1)
        goto NoLoad

        QSHT A 0 A_PlaySound("jkemod/quadshot/load", CHAN_WEAPON)
        QSHT DE 4
        QSHT A 0 A_GiveInventory("QuadShotCounter", 1)
        QSHT A 0 A_TakeInventory("Shell", 4)
        QSHT A 8
        goto Ready

      NoLoad:
        QSHT A 1
        goto Ready


      Spawn:
        QSHT Z -1
        stop
    }
}


actor QuadShotCounter: Ammo
{
    Inventory.MaxAmount 4
    +IGNORESKILL
}
