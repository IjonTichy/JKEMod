actor M249Counter: Ammo
{
    +IGNORESKILL
    Inventory.MaxAmount 100
}

actor "M249 Machine Gun": Weapon
{
    +AMMO_OPTIONAL
    +NOALERT
    Weapon.AmmoType1 "M249Counter"
    Weapon.AmmoType2 "M249Ammo"
    Weapon.AmmoGive1 200
    Weapon.AmmoGive2 100
    Weapon.AmmoUse1 1
    Weapon.SelectionOrder 950

    Weapon.UpSound "jkemod/m249machinegun/up"
    AttackSound "jkemod/m249machinegun/fire"

    Inventory.PickupMessage "$JKE_PK_M249MACHINEGUN"
    Obituary "$JKE_MP_M249MACHINEGUN"
    States
    {
      Select:
        M249 A 1 A_Raise
        loop

      Deselect:
        M249 A 1 A_Lower
        loop

      Ready:
        M249 A 1 A_WeaponReady
        Loop

      Fire:
        M249 A 0 A_JumpIfNoAmmo("AltFire")

        M249 A 0 A_AlertMonsters
        M249 B 0 A_Recoil(0.3)
        M249 B 0 A_FireCustomMissile("AssaultCasing", -48, 0, 7, 6)
        M249 B 3 bright A_FireBullets(4, 2, 1, 8, "Bulletpuff", 1)

        M249 A 0 A_AlertMonsters
        M249 C 0 A_Recoil(0.3)
        M249 C 0 A_FireCustomMissile("AssaultCasing", -45, 0, 7, 6)
        M249 C 3 bright A_FireBullets(4, 2, 1, 8, "Bulletpuff", 1)

        M249 A 10 A_ReFire
        goto Ready

      AltFire:
        M249 A 0 A_JumpIfInventory("M249Counter", 0, "NoReload")
        M249 A 0 A_JumpIfInventory("M249Ammo", 1, "ReloadStart")
        goto NoReload

      ReloadStart:
        M249 D 5
        M249 E 0 A_PlaySound("jkemod/m249machinegun/open", CHAN_WEAPON)
        M249 EFGHIJ 5
        M249 K 0 A_PlaySound("jkemod/m249machinegun/load1", CHAN_WEAPON)
        M249 K 5
        M249 L 35
        M249 M 0 A_PlaySound("jkemod/m249machinegun/load2", CHAN_WEAPON)
        M249 MNO 5
        M249 P 0 A_PlaySound("jkemod/m249machinegun/close", CHAN_WEAPON)
        M249 PQRS 5
        goto ReloadLoop

      ReloadLoop:
        M249 S 0 A_GiveInventory("M249Counter", 1)
        M249 S 0 A_TakeInventory("M249Ammo", 1)
        M249 S 0 A_JumpIfInventory("M249Counter", 100, "ReloadEnd")
        M249 S 0 A_JumpIfInventory("M249Ammo", 1, "ReloadLoop")
        goto ReloadEnd

      ReloadEnd:
        M249 A 4
        goto Ready

      NoReload:
        M249 A 1
        M249 A 0 A_ReFire
        goto Ready

      Spawn:
        M249 Z -1
        stop
    }
}
