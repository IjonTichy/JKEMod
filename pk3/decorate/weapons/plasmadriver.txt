
actor " Plasma Driver ": Weapon
{
    Weapon.AmmoType1 "Cell"
    Weapon.AmmoType2 "Cell"
    Weapon.AmmoUse1 2
    Weapon.AmmoUse2 2
    Weapon.AmmoGive 80

    Weapon.SelectionOrder 500

    Inventory.PickupMessage "$JKE_PK_PLASMADRIVER"
    Obituary "$JKE_MP_PLASMADRIVER"

    States
    {
      Select:
        PRFG A 1 A_Raise
        loop

      Deselect:
        PRFG A 1 A_Lower
        loop

      Ready:
        PRFG A 1 A_WeaponReady
        Loop

      Fire:
        PRFG D 2 bright A_FireCustomMissile("PlasmaDriverShot", 0, 1, 5, 0)
        goto GenericFire

      AltFire:
        PRFG D 2 bright A_RailAttack(50, 5, 1, "90 30 30", "FF 00 00")
        goto GenericFire

      GenericFire:
        PRFG EFG 2 bright
        PRFG G 0 A_ReFire
        PRFG C 25
        goto Ready

      Spawn:
        PRFG Z -1
        stop
    }
}


actor PlasmaDriverShot
{
    Radius 8
    Height 8
    Speed 40

    Projectile
    Damage 8

    RenderStyle Add
    Alpha 0.9

    SeeSound "jkemod/plasmadriver/fire"
    DeathSound "jkemod/plasmadriver/explode"
    Obituary "$JKE_MP_PLASMADRIVER_PLASMA"

    States
    {
      Spawn:
        RPLS A 2 bright
        RPLS A 2 bright A_SpawnItemEx("PlasmaDriverTrail")
        RPLS B 2 bright
        RPLS B 2 bright A_SpawnItemEx("PlasmaDriverTrail")
        loop

      Death:
        RPLS CD 3 bright
        RPLS B 0 A_CustomMissile("PlasmaDriverShotSmall", 0, 0, 45,  CMF_AIMDIRECTION|CMF_TRACKOWNER, 0)
        RPLS B 0 A_CustomMissile("PlasmaDriverShotSmall", 0, 0, 135, CMF_AIMDIRECTION|CMF_TRACKOWNER, 0)
        RPLS B 0 A_CustomMissile("PlasmaDriverShotSmall", 0, 0, 225, CMF_AIMDIRECTION|CMF_TRACKOWNER, 0)
        RPLS B 0 A_CustomMissile("PlasmaDriverShotSmall", 0, 0, 315, CMF_AIMDIRECTION|CMF_TRACKOWNER, 0)
        RPLS EFG 3 bright
        stop
    }
}

actor PlasmaDriverShotSmall: PlasmaDriverShot
{
    Radius 5
    Height 5

    Damage 2

    Alpha 0.7
    Scale 0.4

    SeeSound "jkemod/assorted/none"
    States
    {
      Spawn:
        RPLS AB 4 bright
        loop

      Death:
        RPLS CDEFG 3 bright
        stop
    }
}

actor PlasmaDriverTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add
    Alpha 0.5

    States
    {
      Spawn:
        RPLS ABAB 4 bright
        stop
    }
}