actor SuperShotNuke: Weapon
{
    Weapon.AmmoType1 "NukeShell"
    Weapon.AmmoType2 "SuperShotNukeCounter"
    Weapon.AmmoUse1 0
    Weapon.AmmoUse2 0
    Weapon.AmmoGive1 2
    Weapon.AmmoGive2 0

    Weapon.SelectionOrder 800
    Weapon.Kickback 2000

    Inventory.PickupMessage "$JKE_PK_SUPERSHOTNUKE"
    Obituary "$JKE_MP_SUPERSHOTNUKE"

    States
    {
      Select:
        PKS2 A 1 A_Raise
        loop
      Deselect:
        PKS2 A 1 A_Lower
        loop

      Ready:
        PKS2 A 1 A_WeaponReady
        Loop

      Fire:
        PKS2 A 0 A_JumpIfInventory("SuperShotNukeCounter", 0, "RealFire")
        goto NoLoad

      RealFire:
        PKS2 A 0 A_Stop
        PKS2 A 0 ThrustThing(angle*256/360, 64*-cos(pitch), 0, 0)
        PKS2 A 0 ThrustThingZ(0, 256*sin(pitch), 0, 1)
        PKS2 A 0 A_PlaySound("jkemod/supershotnuke/fire", CHAN_WEAPON)
        PKS2 A 0 A_TakeInventory("SuperShotNukeCounter", 10)
        PKS2 A 0 A_FireBullets(16, 4, 1000, 250, "SuperShotNukePuff", 1)
        PKS2 C 1 bright
        PKS2 E 140
        PKS2 D 35
        PKS2 C 18
        PKS2 B 9
        Goto Ready

      AltFire:
        PKS2 A 0 A_JumpIfInventory("SuperShotNukeCounter", 0, "NoLoad")
        PKS2 A 0 A_JumpIfInventory("NukeShell", 2, "Load")
        Goto NoLoad

      Load:
        PKS2 BCD 2
        PKS2 E 6
        PKS2 F 3 A_PlaySound("jkemod/supershotnuke/open", CHAN_WEAPON)
        PKS2 GH 2
        PKS2 I 1
        NKS2 JK 1
        PKS2 L 0 A_GiveInventory("SuperShotNukeCounter", 2)
        PKS2 L 0 A_TakeInventory("NukeShell", 2)
        NKS2 L 3 A_PlaySound("jkemod/supershotnuke/load", CHAN_WEAPON)
        NKS2 MN 2
        NKS2 OP 1
        PKS2 Q 1
        PKS2 R 0 A_JumpIfInventory("AltFired", 1, "CheckKeepLoading")
        goto BackToLoad

      BackToLoad:
        PKS2 R 3 A_PlaySound("jkemod/supershotnuke/close", CHAN_WEAPON)
        PKS2 S 3
        PKS2 T 1
        Goto Ready

      CheckKeepLoading:
        PKS2 A 0 A_JumpIfInventory("SuperShotNukeCounter", 0, "BackToLoad")
        PKS2 A 0 A_JumpIfInventory("NukeShell", 2, "KeepLoading")
        goto BackToLoad

      KeepLoading:
        PKS2 R 0 A_PlaySound("jkemod/supershotnuke/close", CHAN_WEAPON)
        Goto Load


      NoFire:
      NoLoad:
        PKS2 A 1
        PKS2 A 0 A_ReFire
        goto Ready

      Spawn:
        SGN2 A -1
        stop
    }
}


actor SuperShotNukePuff: BulletPuff
{
    VSpeed 0
    RenderStyle Add
    +PUFFGETSOWNER
    +FORCERADIUSDMG
    +NODAMAGETHRUST
    +PUFFONACTORS
    +ALWAYSPUFF

    Scale 0.7
    States
    {
      Spawn:
        SOD0 EEEEEEEEEEEEFFFFFFFFFFFFGGGGGGGGGGGGHHHHHHHHHHHHIIIIIIIIIIIIJJJJJJJJJJJJKKKKKKKKKKKKLLLLLLLLLLLLMMMMMMMMMMMMNNNNNNNNNNNNOOOOOOOOOOO 2 bright A_Explode(1024, 512)
        Stop
    }
}

actor SuperShotNukeCounter: Ammo
{
    +IGNORESKILL
    Inventory.MaxAmount 10
}
