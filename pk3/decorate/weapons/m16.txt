actor M16: Weapon
{
    Scale 0.2
    +AMMO_OPTIONAL

    weapon.selectionorder 400

    weapon.AmmoGive1 80
    Weapon.AmmoGive2 80
    weapon.AmmoType1 "M16Counter"
    Weapon.AmmoType2 "Clip"
    weapon.AmmoUse1 1

    Inventory.PickupMessage "$JKE_PK_M16"
    Obituary "$JKE_MP_M16"

    States
    {
      Select:
        RIFL A 1 A_Raise
        Loop
      Deselect:
        RIFL A 1 A_Lower
        Loop

      Ready:
        RIFL A 1 A_WeaponReady
        Loop
      Fire:
        RIFL A 0 A_JumpIfNoAmmo("AltFire")
        RIFL B 0 A_PlaySound("jkemod/m16/fire", CHAN_WEAPON)
        RIFL B 0 bright A_FireBullets(3, 2, 1, 8, "BulletPuff", 1)
        RIFL B 2 bright A_Light1
        RIFL C 2 A_FireCustomMissile("AssaultCasing", 55, 0, 3, 7)
        RIFL D 0 A_ReFire
        RIFL D 3 A_Light0
        RIFL E 3
        Goto Ready

      AltFire:
      ReloadBegin:
        RIFL A 0 A_JumpIfInventory("M16Counter", 0, "NoReload")
        RIFL A 0 A_JumpIfInventory("Clip", 1, 1)
        Goto NoReload

        RIFL GHI 5
        RIFL J 5 A_PlaySound("jkemod/m16/out", CHAN_WEAPON)
        RIFL K 5
        RIFL L 10
        RIFL KJI 5
        goto ReloadLoop

      ReloadLoop:
        RIFL I 0 A_TakeInventory("Clip", 1)
        RIFL I 0 A_GiveInventory("M16Counter", 1)
        RIFL I 0 A_JumpIfInventory("M16Counter", 0, "ReloadEnd")
        RIFL I 0 A_JumpIfInventory("Clip", 1, "ReloadLoop")
        goto ReloadEnd

      ReloadEnd:
        RIFL M 0 A_PlaySound("jkemod/m16/in", CHAN_WEAPON)
        RIFL MNOPQRST 4
        Goto Ready

      NoReload:
        RIFL A 1
        RIFL A 0 A_ReFire
        goto Ready

      Spawn:
        RIFL Z -1
        stop
    }
}

Actor M16Counter: Ammo
{
    +IGNORESKILL
    Inventory.MaxAmount 40
}
