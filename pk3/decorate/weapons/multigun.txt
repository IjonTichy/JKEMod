actor Multigun: Weapon
{

    Weapon.AmmoType1 "ExplosiveAmmo"
    Weapon.AmmoUse1 1
    Weapon.AmmoGive1 10

    +NOAUTOFIRE
    +NOALERT
    Weapon.SelectionOrder 800

    Inventory.PickupMessage "$JKE_PK_MULTIGUN"
    Obituary "$JKE_MP_MULTIGUN"

    States
    {
      Select:
        SBRG A 1 A_Raise
        loop

      Deselect:
        SBRG A 1 A_Lower
        loop

      Ready:
        SBRG A 1 A_WeaponReady
        Loop

      Fire:
        SBRG A 0 A_JumpIfInventory("MultigunFired", 1, "GenericLoad")

        SBRG A 0 A_AlertMonsters
        SBRG A 0 A_JumpIfInventory("MultigunMode", 2, "FlameFire")
        SBRG A 0 A_JumpIfInventory("MultigunMode", 1, "IceFire")
        goto RocketFire

      RocketFire:
        SBRG A 0 A_FireCustomMissile("MultigunRocket", 0, 1, 4, 1)
        goto GenericFire

      IceFire:
        SBRG A 0 A_PlaySound("jkemod/multigun/icefire", CHAN_WEAPON)
        SBRG A 0 A_FireCustomMissile("MultigunIce", 0, 1, 4, 4)
        SBRG A 0 A_FireCustomMissile("MultigunIce", 6, 0, 4, 1)
        SBRG A 0 A_FireCustomMissile("MultigunIce", -5, 0, 4, 3)
        SBRG A 0 A_FireCustomMissile("MultigunIce", 2, 0, 4, 5)
        SBRG A 0 A_FireCustomMissile("MultigunIce", -2, 0, 4, 0)
        SBRG A 0 A_FireCustomMissile("MultigunIce", -4, 0, 4, 2)
        goto GenericFire

      FlameFire:
        SBRG A 0 A_FireCustomMissile("MultigunFlameGrenade", 0, 1, 4, 2)
        goto GenericFire


      GenericFire:
        SBRG A 0 A_GiveInventory("MultigunFired")
        SBRG BC 3 bright
        SBRG D 3
        SBRG A 2
        goto Ready

      GenericLoad:
        SBRG EF 2
        SBRG A 0 A_TakeInventory("MultigunFired")
        SBRG G 4 A_PlaySound("jkemod/multigun/load", CHAN_WEAPON)
        SBRG FED 2
        goto Ready

      AltFire:
        SBRG A 0 A_TakeInventory("MultigunFired")
        SBRG A 0 ACS_ExecuteAlways(901)
        SBRG EFGG 2
        SBRR A 1
        SBRR BCDEFGMMMH 2
        goto AltSecond

      AltSecond:
        SBRR IJ 2
        SBRR K 0 A_PlaySound("jkemod/multigun/switch", CHAN_WEAPON)
        SBRR KLKJIH 2
        SBRR A 0 A_ReFire
        SBRR GFEDBA 2
        SBRG GFE 2
        goto Ready

      AltHold:
        SBRG A 0 ACS_ExecuteAlways(901)
        goto AltSecond


      Spawn:
        SBRG Z -1
        stop
    }
}

actor MultigunFired: Inventory
{
    Inventory.MaxAmount 1
    +UNDROPPABLE
}

actor MultigunMode: Inventory
{
    Inventory.MaxAmount 2
    +UNDROPPABLE
}

actor MultigunRocket
{
    Radius 8
    Height 8
    Speed 30

    Projectile
    Damage 20

    +FORCERADIUSDMG
    +DEHEXPLOSION

    SeeSound "jkemod/multigun/rocketfire"
    DeathSound "jkemod/multigun/rocketexplode"
    Obituary "$JKE_MP_MULTIGUN_ROCKET"

    States
    {
      Spawn:
        MGMS A 1 bright
        loop
      Death:
        BNG4 A 2 A_Explode(160, 128)
        BNG4 BCDEFGHIJKLM 3
        Stop
    }
}

actor MultigunIce
{
    Radius 5
    Height 5
    Speed 20

    Projectile
    Damage (45)
    DamageType Ice

    DeathSound "jkemod/multigun/iceexplode"
    Obituary "$JKE_MP_MULTIGUN_ICE"

    States
    {
      Spawn:
        SHRD ABC 3
        loop
      Death:
        SHEX ABCDE 3
        stop
    }
}

actor MultigunFlameGrenade
{
    Radius 6
    Height 6
    Speed 30
    Gravity 0.5

    Projectile
    Damage 15

    -NOGRAVITY
    +DEHEXPLOSION

    SeeSound "jkemod/multigun/flamefire"
    DeathSound "jkemod/multigun/flameexplode"
    Obituary "$JKE_MP_MULTIGUN_FLAME"

    States
    {
      Spawn:
        MGFG AB 3
        loop
      Death:
        TNT1 A 0 A_CustomMissile("MultigunFire", 0, 0, 0,   CMF_AIMDIRECTION|CMF_TRACKOWNER, 0)
        TNT1 A 0 A_CustomMissile("MultigunFire", 0, 0, 45,  CMF_AIMDIRECTION|CMF_TRACKOWNER, 0)
        TNT1 A 0 A_CustomMissile("MultigunFire", 0, 0, 90,  CMF_AIMDIRECTION|CMF_TRACKOWNER, 0)
        TNT1 A 0 A_CustomMissile("MultigunFire", 0, 0, 135, CMF_AIMDIRECTION|CMF_TRACKOWNER, 0)
        TNT1 A 0 A_CustomMissile("MultigunFire", 0, 0, 180, CMF_AIMDIRECTION|CMF_TRACKOWNER, 0)
        TNT1 A 0 A_CustomMissile("MultigunFire", 0, 0, 225, CMF_AIMDIRECTION|CMF_TRACKOWNER, 0)
        TNT1 A 0 A_CustomMissile("MultigunFire", 0, 0, 270, CMF_AIMDIRECTION|CMF_TRACKOWNER, 0)
        TNT1 A 0 A_CustomMissile("MultigunFire", 0, 0, 315, CMF_AIMDIRECTION|CMF_TRACKOWNER, 0)
        TNT1 A 0 A_Explode(96, 96)

        BNG4 A 0 A_ChangeFlag("NOGRAVITY", 0)
        BNG4 A 2 bright A_Stop
        BNG4 BCDEFGHIJKLM 3 bright
        stop
    }
}

actor MultigunFire
{
    Radius 6
    Height 16
    Speed 2

    Projectile

    +FORCERADIUSDMG
    +NODAMAGETHRUST
    +RIPPER
    +FLOORHUGGER

    Renderstyle Add

    SeeSound "jkemod/multigun/flame"
    Obituary "$JKE_MP_MULTIGUN_FLAME"

    States
    {
      Spawn:
        FIRE ABABCBCBCDCDCDEDEDEFEFGFG 2 bright A_Explode(2, 64)
        FIRE HGH 2 bright
        stop
    }
}
