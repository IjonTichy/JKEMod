actor " Karasawa ": Weapon
{
    Weapon.AmmoType1 "Cell"
    Weapon.AmmoUse1 3
    Weapon.AmmoGive1 60

    Weapon.SelectionOrder 1500

    Decal PlasmaScorch

    Inventory.PickupMessage "$JKE_PK_KARASAWA"
    Obituary "$JKE_MP_KARASAWA"

    States
    {
      Select:
        KSAW A 1 A_Raise
        loop

      Deselect:
        KSAW A 0 A_StopSoundEx("SoundSlot5")
        KSAW A 1 A_Lower
        loop

      Ready:
        KSAW A 0 A_PlaySoundEx("jkemod/karasawa/ready", "SoundSlot5")
        KSAW ABCB 6 A_WeaponReady
        Loop

      Fire:
        KSAW A 0 A_StopSoundEx("SoundSlot5")
        KSAW DE 2 bright
        KSAW F 1 bright A_FireCustomMissile("KarasawaShot")
        KSAW G 1 bright
        KSAW H 1 bright A_ReFire
        KSAW I 10
        KSAW J 0 A_PlaySound("jkemod/karasawa/charge", CHAN_WEAPON)
        KSAW NMLKJ 4
        goto Ready

      Spawn:
        KSWP A -1
        stop
    }
}

actor KarasawaShot
{
    Radius 6
    Height 8
    Speed 70

    Projectile
    Damage 12

    +HEXENBOUNCE
    +RIPPER
    +SPAWNSOUNDSOURCE

    BounceFactor 0.975
    WallBounceFactor 0.975
    ReactionTime 10

    RenderStyle Add
    Alpha 0.8

    SeeSound   "jkemod/karasawa/fire"
    DeathSound "jkemod/karasawa/hit"

    States
    {
      Spawn:
        KWBB AAABBBCCCDDD 1 bright A_SpawnItemEx("KarasawaTrail")
        KWBB A 0 A_Countdown
        loop
      Death:
        KWBD ABCDEF 3 bright
        stop
    }
}

actor KarasawaTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add

    States
    {
      Spawn:
        KWBR AB 4 bright
        stop
    }
}
