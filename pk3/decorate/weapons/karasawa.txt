actor " Karasawa ": Weapon
{
    Weapon.AmmoType1 "Cell"
    Weapon.AmmoUse1 6
    Weapon.AmmoGive1 60

    Weapon.SelectionOrder 1500

    Weapon.SisterWeapon " Power Karasawa "

    Inventory.PickupMessage "$JKE_PK_KARASAWA"
    Obituary "$JKE_MP_KARASAWA"

    States
    {
      Select:
        KSAW A 1 A_Raise
        loop

      Deselect:
        KSAW A 0 A_StopSoundEx("SoundSlot5")
        KSAW A 1 A_Lower
        loop

      Ready:
        KSAW A 0 A_PlaySoundEx("jkemod/karasawa/ready", "SoundSlot5")
        KSAW ABCB 6 A_WeaponReady
        Loop

      Fire:
        KSAW A 0 A_StopSoundEx("SoundSlot5")
        KSAW DE 2 bright
        KSAW F 1 bright A_FireCustomMissile("KarasawaShot")
        KSAW G 1 bright
        KSAW H 1 bright A_ReFire
        KSAW I 10
        KSAW J 0 A_PlaySound("jkemod/karasawa/charge", CHAN_WEAPON)
        KSAW NMLKJ 4
        goto Ready

      Spawn:
        KSWP A -1
        stop
    }
}


actor " Power Karasawa ": " Karasawa "
{
    Weapon.AmmoUse1 15
    Weapon.AmmoGive1 0

    Weapon.SisterWeapon " Karasawa "
    +POWERED_UP

    Obituary "$JKE_MP_KARASAWA_POWER"

    States
    {
      Fire:
        KSAW A 0 A_StopSoundEx("SoundSlot5")
        KSAW DE 2 bright
        KSAW F 3 bright A_FireCustomMissile("PowerKarasawaShot")
        KSAW G 4 bright
        KSAW H 4 bright A_ReFire
        KSAW I 10
        KSAW J 0 A_PlaySound("jkemod/karasawa/charge", CHAN_WEAPON)
        KSAW NMLKJ 4
        goto Ready
    }
}


actor KarasawaShot
{
    Radius 6
    Height 8
    Speed 70

    Projectile
    Damage (20)

    +HEXENBOUNCE
    +RIPPER
    +SPAWNSOUNDSOURCE

    BounceFactor 0.95
    WallBounceFactor 0.95
    ReactionTime 10

    RenderStyle Add
    Alpha 0.8

    Decal PlasmaScorch

    SeeSound   "jkemod/karasawa/fire"
    DeathSound "jkemod/karasawa/hit"

    States
    {
      Spawn:
        KWBB AAABBBCCCDDD 1 bright A_SpawnItemEx("KarasawaTrail")
        KWBB A 0 A_Countdown
        loop
      Death:
        KWBD ABCDEF 3 bright
        stop
    }
}

actor KarasawaTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add

    States
    {
      Spawn:
        KWBR AB 4 bright
        stop
    }
}


actor PowerKarasawaShot: KarasawaShot
{
    +FORCERADIUSDMG
    -HEXENBOUNCE
    -RIPPER

    Obituary "$JKE_MP_KARASAWA_POWER"

    States
    {
      Spawn:
        KWBB ABCD 3 bright A_SpawnItemEx("KarasawaTrail")
        loop
      Death:
        KWBD A 0 A_Explode(96, 96, 0)
        KWBD A 0 Bright A_SpawnItemEx("PowerKarasawaShotSplit", 0, 0, 0, 40, 0, random(-3,3), -45)
        KWBD A 0 Bright A_SpawnItemEx("PowerKarasawaShotSplit", 0, 0, 0, 40, 0, random(-3,3), -15)
        KWBD A 0 Bright A_SpawnItemEx("PowerKarasawaShotSplit", 0, 0, 0, 40, 0, random(-3,3), 15)
        KWBD A 0 Bright A_SpawnItemEx("PowerKarasawaShotSplit", 0, 0, 0, 40, 0, random(-3,3), 45)
        KWBD ABCDEF 3 bright
        stop
    }
}

actor PowerKarasawaShotSplit: KarasawaShot
{
    +FORCERADIUSDMG
    +PUFFGETSOWNER

    Obituary "$JKE_MP_KARASAWA_POWER"

    States
    {
      Death:
        KWBD A 0 A_Explode(96, 96, 0)
        KWBD ABCDEF 3 bright
        stop
    }
}
