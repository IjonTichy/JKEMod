actor P-200 : Weapon
{
    Weapon.AmmoType1 "Cell"
    Weapon.AmmoType2 "Cell"
    Weapon.AmmoUse1 12
    Weapon.AmmoUse2 1
    Weapon.AmmoGive 50

    Weapon.SelectionOrder 900
    Inventory.PickupMessage "$JKE_PK_P200"
    Obituary "$JKE_MP_P200"
    States
    {
      Select:
        P200 A 1 A_Raise
        loop
      Deselect:
        P200 A 1 A_Lower
        loop

      Ready:
        P200 A 1 A_WeaponReady
        Loop

      Fire:
        P200 B 0 A_PlaySound("jkemod/p200/fire", CHAN_WEAPON)
        P200 B 0 A_FireCustomMissile("P200Shot", random(-50, 50)/10.0, 1, 5, -1, 0, random(-30, 30)/10.0)
        P200 BBBBBBBBBBBBBBBBBBB 0 A_FireCustomMissile("P200Shot", random(-50, 50)/10.0, 0, 5, -1, 0, random(-30, 30)/10.0)
        P200 BC 0 A_GunFlash
        P200 BCDEFGGGGGGGGGG 2
        P200 A 1 A_ReFire
        goto Ready

      AltFire:
        P200 B 0 A_PlaySound("jkemod/p200/altfire", CHAN_WEAPON)
        P200 B 0 A_FireCustomMissile("P200Shot2", 0, 1, 5, 2)
        P200 B 0 A_GunFlash
        P200 BC 2 bright
        P200 B 0 A_ReFire
        P200 EF 2
        P200 G 2
        goto Ready

      AltFlash:
        P20F A 2 bright A_Light2
        goto LightDone

      Spawn:
        P200 Z -1
        stop
    }
}

actor P200Shot
{
    Radius 5
    Height 5
    Speed 60

    Projectile
    Damage 0
    +FORCERADIUSDMG
    +NODAMAGETHRUST

    Scale 0.6
    RenderStyle Add
    Translation "112:127=250:254"

    DeathSound "jkemod/p200/explode"
    States
    {
      Spawn:
        CSSF D 0 A_SpawnItemEx("P200Trail", 0, 0, 0, 0, 0, 0, 0, 128, 0)
        CSSF D 2 A_BishopMissileWeave
        CSSF E 0 A_SpawnItemEx("P200Trail", 0, 0, 0, 0, 0, 0, 0, 128, 0)
        CSSF E 2 A_BishopMissileWeave
        loop

      Death:
        CSSF F 0 A_Explode(24, 96, 0)
        CSSF FGHI 3 bright
        stop
    }
}

actor P200Shot2: P200Shot
{
    Scale 1

    States
    {
      Spawn:
        CSSF DE 2 bright A_SpawnItemEx("P200Trail2", 0, 0, 0, 0, 0, 0, 0, 128, 0)
        loop

      Death:
        CSSF F 0 A_Explode(48, 96, 0)
        CSSF FGHI 3 bright
        stop
    }
}

actor P200Trail
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add
    Alpha 0.3
    Translation "112:127=250:254"

    Scale 0.3
    States
    {
      Spawn:
        CSSF DEDED 2 bright
        stop
    }
}


actor P200Trail2: P200Trail
{
    Scale 0.5
}
