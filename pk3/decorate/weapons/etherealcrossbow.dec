actor " Ethereal Crossbow ": Weapon
{
    Weapon.AmmoType1 "EtherealAmmo"
    Weapon.AmmoType2 "EtherealAmmo"
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 2
    Weapon.AmmoGive1 10

    Weapon.SisterWeapon " Power Ethereal Crossbow "

    Weapon.SelectionOrder 800
    Weapon.YAdjust 15

    Inventory.PickupMessage "$JKE_PK_ETHEREALCROSSBOW"

    States
    {
      Select:
        MCRB A 1 bright A_Raise
        loop

      Deselect:
        MCRB A 1 bright A_Lower
        loop

      Ready:
        MCRB AAAAAABBBBBBCCCCCC 1 bright A_WeaponReady
        loop

      Fire:
        MCRB D 0 A_PlaySound("jkemod/etherealcrossbow/fire", CHAN_WEAPON)
        MCRB D 0 A_FireCustomMissile("ECBowShot", 0, 1)
        MCRB D 0 A_FireCustomMissile("ECBowShotSide2", -15, 0)
        MCRB D 0 A_FireCustomMissile("ECBowShotSide1",  -3, 0)
        MCRB D 0 A_FireCustomMissile("ECBowShotSide1",   3, 0)
        MCRB D 0 A_FireCustomMissile("ECBowShotSide2",  15, 0)
        MCRB D 6 bright
        MCRB EFGH 3 bright
        MCRB AB 4 bright
        MCRB C 5 bright A_ReFire
        goto Ready

      AltFire:
        MCRB D 0 A_PlaySound("jkemod/etherealcrossbow/fire", CHAN_WEAPON)
        MCRB D 0 A_FireCustomMissile("ECBowAltShot", 0, 1)
        MCRB D 0 A_FireCustomMissile("ECBowAltShot", -15, 0)
        MCRB D 0 A_FireCustomMissile("ECBowAltShot2", -8, 0)
        MCRB D 0 A_FireCustomMissile("ECBowAltShot2", 8, 0)
        MCRB D 0 A_FireCustomMissile("ECBowAltShot", 15, 0)
        MCRB D 8 bright
        MCRB EFGH 4 bright
        MCRB AB 4 bright
        MCRB C 5 bright A_ReFire
        goto Ready

      Spawn:
        MCRB Z -1
        stop
    }
}

actor ECBowShot
{
    Radius 11
    Height 8
    Speed 40

    Projectile
    Damage (40+random(0,12))

    RenderStyle Add

    DeathSound "jkemod/etherealcrossbow/hit"

    Decal CrossbowScorch

    Obituary "$JKE_MP_ETHEREALCROSSBOW_PRIMARY"

    States
    {
      Spawn:
        MCBS B -1 bright
        loop

      Death:
        MCBS H 0
        MCBS HIJ 8 bright
        stop
    }
}

actor ECBowShotSide1: ECBowShot
{
    Speed 35
    Damage (25+random(0,15))

    Decal CrossbowScorch2

    +WINDTHRUST

    States
    {
      Spawn:
        MCBS A -1 bright
        loop

      Death:
        MCBS CDE 8 bright
        Stop
    }
}

actor ECBowShotSide2: ECBowShotSide1
{
    Scale 0.7
    Damage (random(0,7))

    States
    {
      Spawn:
        MCBS A -1 bright
        loop

      Death:
        MCBS CDE 8 bright
        Stop
    }
}

actor ECBowAltShot
{
    Radius 11
    Height 8
    Speed 40

    Projectile
    Damage (17+random(0,6))

    RenderStyle Add

    Decal CrossbowScorch

    DeathSound "jkemod/etherealcrossbow/hit"
    Obituary "$JKE_MP_ETHEREALCROSSBOW_ALT"

    States
    {
      Spawn:
        MCBS B 4 bright
        goto Death

      Death:
        MCBS H 0 A_Stop
        MCBS H 0 A_Explode(60, 96, 0)
        MCBS HIJ 8 bright
        stop
    }
}

actor ECBowAltShot2: ECBowAltShot
{
    Speed 35
    Damage (12+random(0,6))

    Decal CrossbowScorch

    +WINDTHRUST

    States
    {
      Spawn:
        MCBS B 6 bright
        goto Death

      Death:
        MCBS H 0 A_Stop
        MCBS H 0 A_Explode(20, 96, 0)
        MCBS CDE 8 bright
        Stop
    }
}


actor " Power Ethereal Crossbow ": " Ethereal Crossbow "
{
    Weapon.AmmoGive1 0

    Weapon.SisterWeapon " Ethereal Crossbow "
    +POWERED_UP

    States
    {
      Fire:
        MCRB D 0 A_PlaySound("jkemod/etherealcrossbow/fire", CHAN_WEAPON)
        MCRB D 0 A_FireCustomMissile("PowerECBowShot", 0, 1)
        MCRB D 0 A_FireCustomMissile("PowerECBowShotSide2", -15, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowShotSide1",  -6, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowShotSide1",  -3, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowShotSide1",  -1, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowShotSide1",   1, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowShotSide1",   3, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowShotSide1",   6, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowShotSide2",  15, 0)
        MCRB D 6 bright
        MCRB EFGH 3 bright
        MCRB AB 4 bright
        MCRB C 5 bright A_ReFire
        goto Ready

      AltFire:
        MCRB D 0 A_PlaySound("jkemod/etherealcrossbow/fire", CHAN_WEAPON)
        MCRB D 0 A_FireCustomMissile("PowerECBowAltShot", 0, 1)
        MCRB D 0 A_FireCustomMissile("PowerECBowAltShot2", -15, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowAltShot", -12, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowAltShot2", -9, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowAltShot2", -6, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowAltShot3", -3, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowAltShot3", 3, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowAltShot2", 6, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowAltShot2", 9, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowAltShot", 12, 0)
        MCRB D 0 A_FireCustomMissile("PowerECBowAltShot2", 15, 0)
        MCRB D 8 bright
        MCRB EFGH 4 bright
        MCRB AB 4 bright
        MCRB C 5 bright A_ReFire
        goto Ready
    }
}



actor PowerECBowShot
{
    Radius 11
    Height 8
    Speed 40

    Projectile
    Damage (27+random(0,6))

    RenderStyle Add

    Decal CrossbowScorchBig

    DeathSound "jkemod/etherealcrossbow/hit"
    Obituary "$JKE_MP_ETHEREALCROSSBOW_POWER_P"

    States
    {
      Spawn:
        MCBS B 1 bright A_SpawnItemEx("ECBowTrail", 0, random(-4, 4), random(-4, 4), random(-4, 4))
        loop

      Death:
        MCBS HHHH 0 A_SpawnItemEx("ECBowTrail", 0,0,0, random(-5, 5),random(-5, 5),random(-5, 5))
        MCBS H 0 A_Explode(96, 96, 0)
        MCBS HIJ 8 bright
        stop
    }
}

actor PowerECBowShotSide1: ECBowShot
{
    Speed 35
    Damage (18+random(0,6))

    +WINDTHRUST
    +FORCERADIUSDMG

    Decal CrossbowScorch

    States
    {
      Spawn:
        MCBS A -1 bright
        loop

      Death:
        MCBS HH 0 A_SpawnItemEx("ECBowTrail", 0,0,0, random(-5, 5),random(-5, 5),random(-5, 5))
        MCBS A 0 A_Explode(8, 96, 0)
        MCBS CDE 8 bright
        Stop
    }
}

actor PowerECBowShotSide2: ECBowShotSide1
{
    Scale 0.7
    Damage (random(0,4))
    +FORCERADIUSDMG

    Decal CrossbowScorch

    States
    {
      Spawn:
        MCBS A -1 bright
        loop

      Death:
        MCBS HH 0 A_SpawnItemEx("ECBowTrail", 0,0,0, random(-5, 5),random(-5, 5),random(-5, 5))
        MCBS A 0 A_Explode(4, 96, 0)
        MCBS CDE 8 bright
        Stop
    }
}

actor PowerECBowAltShot
{
    Radius 11
    Height 8
    Speed 40

    Projectile
    Damage (17+random(0,6))

    RenderStyle Add
    +FORCERADIUSDMG

    Decal CrossbowScorchBig

    DeathSound "jkemod/etherealcrossbow/hit"
    Obituary "$JKE_MP_ETHEREALCROSSBOW_POWER_A"

    States
    {
      Spawn:
        MCBS B 4 bright
        goto Death

      Death:
        MCBS H 0 A_Stop
        MCBS HHHHHHHH 0 A_SpawnItemEx("ECBowTrail", 0,0,0, random(-5, 5),random(-5, 5),random(-5, 5))
        MCBS H 0 A_Explode(96, 96, 0)
        MCBS HIJ 8 bright
        stop
    }
}

actor PowerECBowAltShot2: PowerECBowAltShot
{
    Speed 35
    Damage (10+random(0,6))

    Decal CrossbowScorchBig

    +WINDTHRUST

    States
    {
      Spawn:
        MCBS B 6 bright
        goto Death

      Death:
        MCBS H 0 A_Stop
        MCBS H 0 A_Explode(48, 96, 0)
        MCBS CDE 8 bright
        Stop
    }
}


actor PowerECBowAltShot3: PowerECBowAltShot
{
    States
    {
      Spawn:
        MCBS B 6 bright
        goto Super::Death
    }
}

actor ECBowTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add

    States
    {
      Spawn:
        MCBS FG 8 bright
        stop
    }
}
