actor FireSSG: Weapon
{
    +WEAPON.NOAUTOAIM
    Weapon.SelectionOrder 390

    Weapon.AmmoUse1 2
    Weapon.AmmoUse2 1
    Weapon.AmmoGive 8
    Weapon.AmmoType1 "Shell"
    Weapon.AmmoType2 "Shell"

    weapon.UpSound "jkemod/firessg/up"

    Inventory.PickupMessage "$JKE_PK_FIRESSG"
    Obituary "$JKE_MP_FIRESSG"

    States
    {
      Ready:
        FSH2 A 1 A_WeaponReady
        Loop
      Deselect:
        FSH2 A 1 A_Lower
      Select:
        FSH2 A 1 A_Raise
        Loop
      Fire:
        FSH2 A 0        A_GunFlash
        FSH2 A 0        A_PlaySound("jkemod/firessg/fire", CHAN_WEAPON)
        FSH2 A        0 A_FireCustomMissile("FireSSGShot1", random(-15, 15), 1,  5, 0, 0, random(-7, 7))
        FSH2 AAAAAAA  0 A_FireCustomMissile("FireSSGShot1", random(-15, 15), 0,  5, 0, 0, random(-7, 7))
        FSH2 AAAAAAAA 0 A_FireCustomMissile("FireSSGShot1", random(-15, 15), 0, -5, 0, 0, random(-7, 7))
        FSH2 A 2 offset(0, 33) bright A_Recoil(5)
        FSH2 A 2 offset(0, 40) bright
        FSH2 A 2 offset(0, 36) bright
        FSH2 B 6
        FSH2 C 5
        FSH2 D 0 A_PlaySound("jkemod/firessg/open", CHAN_WEAPON)
        FSH2 D 6
        FSH2 E 0 A_FireCustomMissile("ShellCasing", 30, 0, 0, 7)
        FSH2 E 0 A_FireCustomMissile("ShellCasing", 33, 0, 0, 7)
        FSH2 E 7
        FSH2 F 0 A_PlaySound("jkemod/firessg/load", CHAN_WEAPON)
        FSH2 F 6
        FSH2 G 5
        FSH2 H 5
        FSH2 A 2 A_ReFire
        FSH2 A 0 A_PlaySound("jkemod/firessg/close", CHAN_WEAPON)
        Goto Ready
      AltFire:
        FSH2 A 0 A_GunFlash
        FSH2 A 0 A_PlaySound("jkemod/firessg/fire", CHAN_WEAPON)
        FSH2 A 0 A_FireCustomMissile("FireSSGShot2", 0, 1, 4, 0)
        FSH2 A 2 offset(0, 33) bright A_Recoil(4)
        FSH2 A 2 offset(0, 40) bright

        FSH2 A 0 A_GunFlash
        FSH2 A 0 A_PlaySound("jkemod/firessg/fire", CHAN_WEAPON)
        FSH2 A 0 A_FireCustomMissile("FireSSGShot2", 0, 1, -4, 0)
        FSH2 A 2 offset(0, 33) bright A_Recoil(4)
        FSH2 A 2 offset(0, 40) bright
        FSH2 A 2 offset(0, 36) bright
        FSH2 B 6
        FSH2 C 5
        FSH2 D 4 A_PlaySound("jkemod/firessg/open", CHAN_WEAPON)
        FSH2 E 0 A_FireCustomMissile("ShellCasing", 30, 0, 0, 7)
        FSH2 E 0 A_FireCustomMissile("ShellCasing", 33, 0, 0, 7)
        FSH2 E 7
        FSH2 F 5 A_PlaySound("jkemod/firessg/load", CHAN_WEAPON)
        FSH2 G 4
        FSH2 H 5
        FSH2 A 2 A_ReFire
        FSH2 A 0 A_PlaySound("jkemod/firessg/close", CHAN_WEAPON)
        Goto Ready

      Flash:
        FSH2 I 2 bright A_Light1
        FSH2 J 2 bright A_Light2
        FSH2 I 2 bright A_Light0
        Goto LightDone

      Spawn:
        FSH2 Z -1
        Stop
    }
}

actor FireSSGShot1: FastProjectile
{
    Height 5
    Radius 5
    Speed 80

    Damage (3 * random(1, 8) )

    States
    {
      Spawn:
        FX12 AB 3 bright
        Loop
      Death:
        FX12 C 0 A_Explode(12, 64, 0)
        FX12 CDEFGH 3 bright
        Stop
    }
}

actor FireSSGShot2: FastProjectile
{
    Height 6
    Radius 6
    Speed 80

    Damage (20 * random(1, 10))

    States
    {
      Spawn:
        FRTB ABC 3 bright
        Loop
      Death:
        FRTB D 0 A_Explode(96, 96)
        FRTB DEFGHI 4 bright
        Stop
    }
}
