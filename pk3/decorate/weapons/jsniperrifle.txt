
actor " J-Sniper Rifle " : Weapon
{

    Weapon.AmmoType1 "JSniperAmmo"
    Weapon.AmmoUse1 1
    Weapon.AmmoGive 15

    Weapon.SelectionOrder 500

    +WEAPON.NOAUTOAIM

    Inventory.PickupMessage "$JKE_PK_JSNIPERRIFLE"
    Obituary "$JKE_MP_JSNIPERRIFLE"

    States
    {
      Deselect:
        SNIP A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
        SNIP A 1 A_Lower
        loop

      Select:
        SNIP AAAAAAAAAAAAAA 1 A_Raise
        SPRL A 4 A_PlaySound("jkemod/jsniperrifle/load", CHAN_WEAPON)
        SPRL B 4
        SPRL C 4
        SPRL D 4
        SPRL E 4
        Goto Ready

      Ready:
        SNIP A 0 A_TakeInventory("JZoomed")
        SNIP A 1 A_WeaponReady
        Loop

      Fire:
        SNIP B 0 A_Light1
        SNIP B 0 A_PlaySound("jkemod/jsniperrifle/fire", CHAN_WEAPON)
        SNIP B 0 A_JumpIfInventory("JZoomed", 1, "ZoomedFire")
        goto UnzoomedFire

      UnzoomedFire:
        SNIP B 3 bright A_FireBullets(12, 12, -1, 100, "JSniperPuff", 1)
        goto FireEnd

      ZoomedFire:
        SNIP B 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
        SNIP B 3 bright A_FireBullets(0, 0, -1, 130, "JSniperPuff", 1)
        goto FireEnd


      FireEnd:
        SNIP C 6 A_Light0
        SNIP A 6
        SPRL A 4 A_PlaySound("jkemod/jsniperrifle/load", CHAN_WEAPON)
        SPRL B 4
        SPRL C 4
        SPRL D 4
        SPRL E 4
        SPRL E 0 A_FireCustomMissile("AssaultCasing", -65, 0, 5, 6)
        SNIP A 8
        Goto Ready

      AltFire:
        SNIP A 0 A_GiveInventory("JZoomed")
        SNIP A 0 A_ZoomFactor(5.0, ZOOM_INSTANT)

        SNIP A 1 A_WeaponReady
        SNIP A 1 A_ReFire

        SNIP A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
        Goto Ready

      Spawn:
        SNRL A -1
        Stop
    }
}

Actor JZoomed: Inventory
{
    Inventory.MaxAmount 1
    +UNDROPPABLE
}

actor JSniperPuff: BulletPuff
{
    +NOEXTREMEDEATH
}
