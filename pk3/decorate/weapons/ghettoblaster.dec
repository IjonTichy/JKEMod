
actor " Ghetto Blaster ": DoomWeapon
{
    Weapon.SelectionOrder 200

    Inventory.PickupMessage "$JKE_PK_GHETTOBLASTER"
    Obituary "$JKE_MP_GHETTOBLASTER"

    Scale 0.5

    States
    {
      Select:
        GBLS A 1 A_Raise
        Loop

      Deselect:
        GBLS A 0 A_StopSoundEx("SoundSlot6")
        goto Deselect2

      Deselect2:
        GBLS A 1 A_Lower
        loop

      Ready:
        GBLS A 0 A_StopSoundEx("SoundSlot6")
        GBLS A 1 A_WeaponReady
        loop

      Fire:
        GBLS A 0 A_PlaySoundEx("jkemod/ghettoblaster/rickroll", "SoundSlot6", 1)
        GBLS A 1 A_FireCustomMissile("GhettoRickroll")
        GBLS A 0 A_Refire
        goto Ready


      AltFire:
        GBLS A 0 A_PlaySound("jkemod/ghettoblaster/explode", CHAN_WEAPON)
        GBLS A 0 A_FireCustomMissile("GhettoExplosion")
        GBLS A 210
        goto Ready

      Spawn:
        GBLS B -1
        Stop
    }
}

actor GhettoBase
{
    Radius 1
    Height 1
    Speed 0

    Projectile
    Damage 0

    +FORCERADIUSDMG
    +NODAMAGETHRUST
}

actor GhettoRickroll: GhettoBase
{
    Obituary "$JKE_MP_GHETTOBLASTER_RICKROLL"
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Explode(32, 256, 0)
        stop
    }
}

actor GhettoExplosion: GhettoBase
{
    Obituary "$JKE_MP_GHETTOBLASTER_EXPLOSION"
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_Explode(4096, 1024, 0)
        stop
    }
}
