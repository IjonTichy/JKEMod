
actor " Striker ": Weapon
{
    +AMMO_OPTIONAL
    Weapon.AmmoGive1 16
    Weapon.AmmoGive2 16
    Weapon.AmmoType1 "StrikerCounter"
    Weapon.AmmoType2 "Shell"
    Weapon.AmmoUse1 1
    Weapon.SelectionOrder 600
    Scale 0.6

    Inventory.PickupMessage "$JKE_PK_STRIKER"
    Obituary "$JKE_MP_STRIKER"
    States
    {
      Select:
        STKK A 1 A_Raise
        loop

      Deselect:
        STKK A 1 A_Lower
        loop

      Ready:
        STKK A 1 A_WeaponReady
        Loop

      Fire:
        STKK A 0 A_JumpIfNoAmmo("AltFire")

        STKK A 0 A_FireCustomMissile("ShellCasing", -48, 0, 3, 6)
        STKK A 0 A_PlaySound("jkemod/striker/fire", CHAN_WEAPON)
        STKK A 0 A_FireBullets(3, 3, 10, 5, "Bulletpuff")
        STKF AB 2 bright A_Light1
        STKK A 4 A_Light0
        goto Ready

      AltFire:
        STKK A 0 A_JumpIfInventory("StrikerCounter", 0, "NoReload")
        STKK A 0 A_JumpIfInventory("Shell", 1, "ReloadStart")
        goto NoReload

      ReloadStart:
        STKK B 3
        STKR A 10
        STKR B 0 A_PlaySound("jkemod/striker/out", CHAN_WEAPON)
        STKR B 3
        STKR CDEFGHIIIIIIIIIIHGFEDC 2
        goto ReloadLoop

      ReloadLoop:
        STKK A 0 A_TakeInventory("Shell", 1)
        STKK A 0 A_GiveInventory("StrikerCounter", 1)
        STKK A 0 A_JumpIfInventory("StrikerCounter", 0, "ReloadEnd")
        STKK A 0 A_JumpIfInventory("Shell", 1, "ReloadLoop")
        goto ReloadEnd

      ReloadEnd:
        STKR B 0 A_PlaySound("jkemod/striker/in", CHAN_WEAPON)
        STKR B 3
        STKR J 0 A_PlaySound("jkemod/striker/load", CHAN_WEAPON)
        STKR JJJKKJJJ 2
        STKK B 3
        STKK A 4
        goto Ready

      NoReload:
        STKK A 1
        STKK A 0 A_ReFire
        goto Ready

      Spawn:
        STKK Z -1
        stop
    }
}

actor StrikerCounter: Ammo
{
    +IGNORESKILL
    Inventory.MaxAmount 16
}