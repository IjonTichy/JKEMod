actor " L-Striker ": Weapon
{
    +AMMO_OPTIONAL
    Weapon.AmmoGive1 16
    Weapon.AmmoGive2 16
    Weapon.AmmoType1 "StrikerCounter"
    Weapon.AmmoType2 "Shell"
    Weapon.AmmoUse1 1
    Weapon.SelectionOrder 600

    +NOALERT

    Weapon.SisterWeapon " Power L-Striker "

    Decal BulletChip
    Scale 0.6

    Inventory.PickupMessage "$JKE_PK_STRIKER"
    Obituary "$JKE_MP_STRIKER"

    States
    {
      Select:
        STKK A 1 A_Raise
        loop

      Deselect:
        STKK A 1 A_Lower
        loop

      Ready:
        STKK A 1 A_WeaponReady
        STKK A 0 A_JumpIfInventory("ReloadButton", 1, "AltFire")
        loop

      Fire:
        STKK A 0 A_JumpIfNoAmmo("AltFire")
        STKK A 0 A_AlertMonsters

        STKK A 0 A_FireCustomMissile("ShellCasing", -48, 0, 3, 6)
        STKK A 0 A_PlaySound("jkemod/striker/fire", CHAN_WEAPON)
        STKK A 0 A_FireBullets(3, 3, 10, 5, "Bulletpuff")
        STKF AB 2 bright A_Light1
        STKK A 4 A_Light0
        goto Ready


      AltFire:
        STKK A 0 A_JumpIfInventory("StrikerCounter", 0, "NoReload")
        STKK A 0 A_JumpIfInventory("Shell", 1, "ReloadStart")
        goto NoReload

      ReloadStart:
        STKK B 3 A_WeaponReady(14)
        STKR A 10 A_WeaponReady(14)
        STKR B 0 A_PlaySound("jkemod/striker/out", CHAN_WEAPON)
        STKR B 3 A_WeaponReady(14)
        STKR CCCDDDDEEEFFGHIIIIIIIII 1 A_WeaponReady(14)
        goto ReloadLoop

      ReloadLoop:
        STKK A 0 A_TakeInventory("Shell", 1)
        STKK A 0 A_GiveInventory("StrikerCounter", 1)
        STKK A 0 A_JumpIfInventory("StrikerCounter", 0, "ReloadEnd")
        STKK A 0 A_JumpIfInventory("Shell", 1, "ReloadLoop")
        goto ReloadEnd

      ReloadEnd:
        STKR IIIIIIIIIIIHGED 1 A_WeaponReady(14)
        STKR C 1 offset(0, 28) A_WeaponReady(14)
        STKR B 0 A_PlaySound("jkemod/striker/in", CHAN_WEAPON)
        STKR B 2 offset(0, 30) A_WeaponReady(14)
        STKR B 1 offset(0, 39) A_WeaponReady(14)
        STKR B 5 A_WeaponReady(14)
        STKR J 0 A_PlaySound("jkemod/striker/load", CHAN_WEAPON)
        STKR JJJKKJJJ 2 A_WeaponReady(14)
        STKK B 3 A_WeaponReady(14)
        STKK A 4 A_WeaponReady(14)
        goto Ready

      NoReload:
        STKK A 1 A_WeaponReady(14)
        STKK A 0 A_ReFire
        goto Ready

      Spawn:
        STKK Z -1
        stop
    }
}

actor " Power L-Striker ": " L-Striker "
{
    Weapon.AmmoUse1 0

    Weapon.SisterWeapon " L-Striker "
    +POWERED_UP

    Obituary "$JKE_MP_STRIKER_POWER"

    States
    {

      Fire:
        STKK A 0 A_JumpIfInventory("StrikerCounter", 1, "HasPrimary")
        STKK A 0 A_JumpIfInventory("Shell", 1, 1)
        goto NoReload

        STKK A 0 A_TakeInventory("Shell", 1)
        goto ActualFire

      HasPrimary:
        STKK A 0 A_TakeInventory("StrikerCounter", 1)
        goto ActualFire

      ActualFire:
        STKK A 0 A_AlertMonsters

        STKK A 0 A_FireCustomMissile("ShellCasing", -48, 0, 3, 6)
        STKK A 0 A_PlaySound("jkemod/striker/fire", CHAN_WEAPON)
        STKK A 0 A_FireBullets(3, 3, 15, 7, "Bulletpuff")
        STKF AB 2 bright A_Light1
        STKK A 2 A_Light0
        goto Ready


      AltFire:
        STKK A 1
        goto Ready
    }
}

actor StrikerCounter: Ammo
{
    +IGNORESKILL
    Inventory.MaxAmount 16
}
