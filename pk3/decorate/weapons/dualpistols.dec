actor " L-Dual Pistols ": Weapon
{
    Weapon.SelectionOrder 920

    Weapon.AmmoType "Clip"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 80

    Decal BulletChip
    Inventory.PickupMessage "$JKE_PK_DUALPISTOLS"
    Obituary "$JKE_MP_DUALPISTOLS"
    States
    {
      Ready:
        PSTG A 1 A_WeaponReady
        Loop
      Deselect:
        PSTG A 0 A_Lower
        PSTG A 1 A_Lower
        Loop
      Select:
        PSTG A 0 A_Raise
        PSTG A 1 A_Raise
        Loop
      Fire:
        PSTG A 0 A_JumpIfInventory("PistolSide", 1, "RightFire")
        goto LeftFire

      LeftFire:
        PSTG E 0 A_PlaySound("jkemod/dualpistols/fire", CHAN_WEAPON)
        PSTG E 3 bright A_FireBullets(3, 3, 1, 6, "BulletPuff", 1)
        PSTG E 1 A_Light1
        PSTG G 1 A_Light0
        PSTG G 0 A_GiveInventory("PistolSide")
        PSTG G 0 A_ReFire
        Goto Ready

      RightFire:
        PSTG E 0 A_PlaySound("jkemod/dualpistols/fire", CHAN_WEAPON)
        PSTG B 1 bright A_FireBullets(3, 3, 1, 6, "BulletPuff", 1)
        PSTG B 1 A_Light1
        PSTG C 3 A_Light0
        PSTG D 4
        PSTG D 0 A_TakeInventory("PistolSide")
        PSTG D 0 A_ReFire
        Goto Ready

      Spawn:
        PISS A -1
        Stop
    }
}

actor PistolSide: Inventory
{
    inventory.maxamount 1
    +UNDROPPABLE
}
