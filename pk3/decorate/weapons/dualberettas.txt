
actor " Dual Berettas ": Weapon
{
    +AMMO_OPTIONAL
    Weapon.AmmoGive1 60
    Weapon.AmmoGive2 60
    Weapon.AmmoType1 "BerettaCounter"
    Weapon.AmmoType2 "Clip"
    Weapon.AmmoUse1 1

    +NOALERT
    Weapon.SelectionOrder 400
    Weapon.SisterWeapon " Power Dual Berettas "

    Decal BulletChip
    Scale 0.2

    Inventory.PickupMessage "$JKE_PK_DUALBERETTAS"
    Obituary "$JKE_MP_DUALBERETTAS"

    States
    {
      Select:
        BRTU AAAAAAAAAAAAAAA 1 A_Raise
        BRTU B 2
        BRTU C 2
        BRTU D 2
        BRTU E 2
        BRTU F 2 A_PlaySound("jkemod/dualberettas/up", CHAN_WEAPON)
        BRTU F 12 A_GunFlash
        Goto Ready

      Deselect:
        BRT2 A 1 A_Lower
        loop

      Flash:     // NOT ACTUALLY A GUN FLASH
        BR2U A 2
        BR2U B 2
        BR2U C 2
        BR2U D 2
        BR2U E 2
        BR2U F 2
        Goto LightDone

      Ready:
        BRT2 A 1 A_WeaponReady
        loop

      Fire:
        BRT2 A 0 A_JumpIfNoAmmo("Reload")
        BRT2 A 0 A_AlertMonsters
        BRT2 A 0 A_PlaySound("jkemod/dualberettas/fire", CHAN_WEAPON)
        BRT2 A 0 A_FireBullets(2, 1, -1, 6, "BulletPuff", 1)

        BRT2 A 0 A_JumpIfInventory("BerettaSide", 1, "LeftFire")
        goto RightFire

      RightFire:
        BRT2 B 3 bright A_GiveInventory("BerettaSide")
        BRT2 C 2 bright A_FireCustomMissile("PistolCasing", 50, 0, 6, -4)
        BRT2 C 0 A_ReFire
        BRT2 D 2
        goto Ready

      LeftFire:
        BRT2 F 3 bright A_TakeInventory("BerettaSide")
        BRT2 G 2 bright A_FireCustomMissile("PistolCasing", -50, 0, -6, -4)
        BRT2 G 0 A_ReFire
        BRT2 H 2
        goto Ready


      Hold:
        BRT2 A 0 A_JumpIfNoAmmo("Reload")
        BRT2 A 0 A_PlaySound("jkemod/dualberettas/fire", CHAN_WEAPON)
        BRT2 A 0 A_FireBullets(2, 1, -1, 6, "BulletPuff", 1)

        BRT2 A 0 A_JumpIfInventory("BerettaSide", 1, "LeftHold")
        goto RightHold

      RightHold:
        BRT2 E 3 bright A_GiveInventory("BerettaSide")
        BRT2 C 2 bright A_FireCustomMissile("PistolCasing", 50, 0, 6, -4)
        BRT2 C 0 A_ReFire
        BRT2 D 2
        goto Ready

      LeftHold:
        BRT2 I 3 bright A_TakeInventory("BerettaSide")
        BRT2 G 2 bright A_FireCustomMissile("PistolCasing", -50, 0, -6, -4)
        BRT2 G 0 A_ReFire
        BRT2 H 2
        goto Ready




      AltFire:
        BRR2 A 0 A_JumpIfInventory("BerettaCounter", 0, "NoReload")

      Reload:
        BRR2 A 0 A_JumpIfInventory("Clip", 1, "ReloadStart")
        Goto NoReload

      ReloadStart:
        BRR2 A 2
        BRR2 B 2 A_PlaySound("jkemod/assorted/click", CHAN_WEAPON)
        BRR2 CDEF 2
        BRR2 G 2 A_PlaySound("jkemod/dualberettas/out", CHAN_WEAPON)
        BRR2 HI 2
        BRR2 J 2 A_PlaySound("jkemod/dualberettas/load1", CHAN_WEAPON)
        BRR2 K 4 A_PlaySound("jkemod/dualberettas/load2", CHAN_WEAPON)
        BRR2 LMN 2
        BRTR ABCDEFG 2
        BRTR J 2 A_PlaySound("jkemod/dualberettas/load1", CHAN_WEAPON)
        BRTR K 4 A_PlaySound("jkemod/dualberettas/load2", CHAN_WEAPON)

      ReloadLoop:
        BRTR K 0 A_TakeInventory("Clip", 1)
        BRTR K 0 A_GiveInventory("BerettaCounter", 1)
        BRTR K 0 A_JumpIfInventory("BerettaCounter", 0, "ReloadEnd")
        BRTR K 0 A_JumpIfInventory("Clip", 1, "ReloadLoop")
        goto ReloadEnd

      ReloadEnd:
        BRT2 A 3
        Goto Ready

      NoReload:
        BRT2 A 1
        BRT2 A 0 A_ReFire
        goto Ready

      Spawn:
        1337 A -1
        stop
    }
}

actor " Power Dual Berettas ": " Dual Berettas "
{
    Weapon.AmmoGive1 0
    Weapon.AmmoGive2 0

    Weapon.SisterWeapon " Dual Berettas "
    +POWERED_UP

    Obituary "$JKE_MP_DUALBERETTAS_POWER"

    states
    {
      Fire:
        BRT2 A 0 A_JumpIfNoAmmo("Reload")
        BRT2 A 0 A_AlertMonsters
        BRT2 A 0 A_PlaySound("jkemod/dualberettas/fire", CHAN_WEAPON)
        BRT2 A 0 A_FireBullets(2, 1, -1, 9, "ExplosiveBulletPuff", 1)

        BRT2 A 0 A_JumpIfInventory("BerettaSide", 1, "LeftFire")
        goto RightFire

      RightFire:
        BRT2 B 2 bright A_GiveInventory("BerettaSide")
        BRT2 C 2 bright A_FireCustomMissile("PistolCasing", 50, 0, 6, -4)
        BRT2 C 0 A_ReFire
        BRT2 D 1
        goto Ready

      LeftFire:
        BRT2 F 2 bright A_TakeInventory("BerettaSide")
        BRT2 G 2 bright A_FireCustomMissile("PistolCasing", -50, 0, -6, -4)
        BRT2 G 0 A_ReFire
        BRT2 H 1
        goto Ready


      Hold:
        BRT2 A 0 A_JumpIfNoAmmo("Reload")
        BRT2 A 0 A_AlertMonsters
        BRT2 A 0 A_PlaySound("jkemod/dualberettas/fire", CHAN_WEAPON)
        BRT2 A 0 A_FireBullets(2, 1, -1, 9, "ExplosiveBulletPuff", 1)

        BRT2 A 0 A_JumpIfInventory("BerettaSide", 1, "LeftHold")
        goto RightHold

      RightHold:
        BRT2 E 2 bright A_GiveInventory("BerettaSide")
        BRT2 C 2 bright A_FireCustomMissile("PistolCasing", 50, 0, 6, -4)
        BRT2 C 0 A_ReFire
        BRT2 D 1
        goto Ready

      LeftHold:
        BRT2 I 2 bright A_TakeInventory("BerettaSide")
        BRT2 G 2 bright A_FireCustomMissile("PistolCasing", -50, 0, -6, -4)
        BRT2 G 0 A_ReFire
        BRT2 H 1
        goto Ready

      Spawn:
        1337 A -1
        stop
    }
}

Actor BerettaCounter: Ammo
{
    +IGNORESKILL
    Inventory.MaxAmount 30
    Inventory.Icon "BCLPA0"
}

actor BerettaSide: Inventory
{
    Inventory.MaxAmount 1
    +UNDROPPABLE
}
