actor " Pyro Cannon ": Weapon
{
    Weapon.AmmoType1 "JKEGas"
    Weapon.AmmoUse1 40
    Weapon.AmmoGive1 120
    
    Weapon.SelectionOrder 1400
    
    Weapon.UpSound "jkemod/pyrocannon/up"
    
    Inventory.PickupMessage "$JKE_PK_PYROCANNON"
    
    States
    {
      Select:
        PYRG A 1 A_Raise
        loop
        
      Deselect:
        PYRG A 1 A_Lower
        loop
        
      Ready:
        PYRG A 1 A_WeaponReady
        Loop
        
      Fire:
        PYRG B 0 A_PlaySound("jkemod/pyrocannon/fire", CHAN_WEAPON)
        PYRG B 3 A_FireCustomMissile("PyroRocket", 0, 1, 0, 0)
        PYRG C 3
        PYRG DEFG 3
        PYRG A 35
        goto Ready
        
      Spawn:
        PYRG Z -1
        stop
    }
}

actor PyroRocket
{
    Radius 6
    Height 6
    Speed 40
    
    Projectile
    
    +FORCERADIUSDMG
    +DEHEXPLOSION
    
    DeathSound "jkemod/assorted/explode"
    
    Obituary "$JKE_MP_PYROCANNON_ROCKET"
    
    States
    {
      Spawn:
        MISL A 1
        loop
      Death:
        PYXP A 0 A_Explode(386, 192)
        PYXP ABCDEFGH 2 bright
        PYXP H 0 A_SpawnItemEx("PyroShrapnel", 0,0,0, 25,0,0, 45)
        PYXP H 0 A_SpawnItemEx("PyroShrapnel", 0,0,0, 25,0,0, 135)
        PYXP H 0 A_SpawnItemEx("PyroShrapnel", 0,0,0, 25,0,0, 225)
        PYXP H 0 A_SpawnItemEx("PyroShrapnel", 0,0,0, 25,0,0, 315)
        PYXP IJKLMNOPQRSTU 2 bright
        stop
    }
}

actor PyroShrapnel
{
    Radius 6
    Height 6
    
    Speed 25
    
    Projectile
    
    +FORCERADIUSDMG
    +PUFFGETSOWNER

    RenderStyle Add
    
    DeathSound "jkemod/assorted/explode"
    
    Obituary "$JKE_MP_PYROCANNON_SHRAPNEL"
    
    States
    {
      Spawn:
        FRTB ABC 3 A_SpawnItemEx("PyroTrail")
        loop
        
      Death:
        MISL B 0 A_Explode(160, 160)
        MISL BCD 4
        MISL D 0 A_SpawnItemEx("PyroFlame", 0,0,0, 2,0,0, 0)
        MISL D 0 A_SpawnItemEx("PyroFlame", 0,0,0, 2,0,0, 45)
        MISL D 0 A_SpawnItemEx("PyroFlame", 0,0,0, 2,0,0, 90)
        MISL D 0 A_SpawnItemEx("PyroFlame", 0,0,0, 2,0,0, 135)
        MISL D 0 A_SpawnItemEx("PyroFlame", 0,0,0, 2,0,0, 180)
        MISL D 0 A_SpawnItemEx("PyroFlame", 0,0,0, 2,0,0, 225)
        MISL D 0 A_SpawnItemEx("PyroFlame", 0,0,0, 2,0,0, 270)
        MISL D 0 A_SpawnItemEx("PyroFlame", 0,0,0, 2,0,0, 315)
        stop
    }
}

actor PyroTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    
    RenderStyle Add
    
    States
    {
      Spawn:
        FRTB ABC 3
        stop
    }
}

actor PyroFlame: MultigunFire    // see multigun.txt
{
    Obituary "$JKE_MP_PYROCANNON_FLAME"
}
