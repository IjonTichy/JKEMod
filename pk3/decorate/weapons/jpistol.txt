Actor JPistolCounter: Ammo
{
    Inventory.MaxAmount 14
}

actor JPistol: Weapon
{
    +AMMO_OPTIONAL
    +NOALERT
    weapon.AmmoGive1 28
    weapon.AmmoType1 "JPistolCounter"
    weapon.AmmoType2 "Clip"
    weapon.AmmoGive2 40
    weapon.AmmoUse1 1
    weapon.selectionorder 1200

    Inventory.PickupMessage "$JKE_PK_JPISTOL"
    Obituary "$JKE_MP_JPISTOL"
    states
    {
      Ready:
        PIS3 A 1 A_WeaponReady
      Select:
        PIS3 A 0 A_Raise
        PIS3 A 1 A_Raise
        loop
      Deselect:
        PIS3 A 0 A_Lower
        PIS3 A 1 A_Lower
        loop
      Fire:
        PIS3 A 0 A_JumpIfNoAmmo("AltFire")

        PIS3 B 0 A_AlertMonsters
        PIS3 B 0 A_PlaySound("jkemod/jpistol/fire", CHAN_WEAPON)
        PIS3 B 2 bright A_FireBullets(2, 2, 1, 6, "Bulletpuff", 1)

        PIS3 C 3 bright
        PIS3 D 4 A_FireCustomMissile("PistolCasing", -45, 0, 6, 0)
        PIS3 A 0 A_ReFire
        goto Ready

      AltFire:
        PIS3 A 0 A_JumpIfInventory("JPistolCounter", 0, "NoReload")
        PIS3 A 0 A_JumpIfInventory("Clip", 1, "ReloadStart")
        goto NoReload

      ReloadStart:
        PIS3 C 2 offset(0, 40)
        PIS3 C 2 offset(0, 50) A_PlaySound("jkemod/assorted/click", CHAN_WEAPON)
        PIS3 C 2 offset(0, 60)
        PIS3 C 30 offset(0, 70)

      ReloadLoop:
        PIS3 A 0 A_JumpIfInventory("JPistolCounter", 0, "ReloadEnd")
        PIS3 A 0 A_JumpIfInventory("Clip", 1, 1)
        goto ReloadEnd
        PIS3 A 0 A_TakeInventory("Clip", 1)
        PIS3 A 0 A_GiveInventory("JPistolCounter", 1)
        goto ReloadLoop

      ReloadEnd:
        PIS3 C 0 A_PlaySound("jkemod/jpistol3/load", CHAN_WEAPON)
        PIS3 C 15 offset(0, 70)
        PIS3 C 2 offset(0, 60)
        PIS3 C 2 offset(0, 50)
        PIS3 C 2 offset(0, 40)
        PIS3 A 2
        goto Ready

      NoReload:
        PIS3 A 1
        PIS3 A 0 A_ReFire
        goto Ready

      Spawn:
        PWFL A -1   // same as Powerful's sprite
        stop
    }
}
