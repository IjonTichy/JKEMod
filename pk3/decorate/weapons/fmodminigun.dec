actor " L-Minigun ": Weapon
{
    Weapon.AmmoType1 "MinigunAmmo"
    Weapon.AmmoType2 "MinigunAmmo"
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 1
    Weapon.AmmoGive 100

    +NOALERT
    Weapon.SelectionOrder 550

    AttackSound "jkemod/fmodminigun/fire"
    
    Weapon.SisterWeapon " Power L-Minigun "

    Decal BulletChip

    Inventory.PickupMessage "$JKE_PK_FMODMINIGUN"
    Obituary "$JKE_MP_FMODMINIGUN"

    States
    {

      Select:
        D3CG A 1 A_Raise
        loop

      Deselect:
        D3CG A 1 A_Lower
        loop

      Ready:
        D3CG A 1 A_WeaponReady
        Loop

      Fire:
        D3CG A 1
        D3CG A 0 A_ReFire
        goto Ready

      AltFire:
        D3CG B 0 A_PlaySound("jkemod/fmodminigun/up", 6)
        D3CG BA 6
        D3CG BA 5
        D3CG BA 4
        D3CG BA 3
        D3CG BA 2
        D3CG BA 1
        D3CG A 0 A_ReFire
        goto Down

      AltHold:
        D3CG A 0 ACS_Execute(876, 0, 1, 0, 0)
        D3CG A 0 A_PlaySound("jkemod/fmodminigun/spin", 6)
        D3CG BABABA 1 A_JumpIfInventory("MainFired", 1, "Shoot")
        D3CG A 0 A_ReFire
        goto Down

      Shoot:
        D3CG A 0 A_AlertMonsters
        D3CG A 0 ACS_ExecuteAlways(876, 0, 0, 20, 0)
        D3CG A 0 Radius_Quake(2, 3, 0, 1, 0)
        D3CG A 0 A_GunFlash

        D3CG B 0 ACS_ExecuteAlways(474, 0, 0, 20)
        D3CG A 1 bright A_FireBullets(5, 5, 1, 10, "BulletPuff")

        D3CG B 0 ACS_ExecuteAlways(474, 0, 0, 20)
        D3CG B 1 bright A_FireBullets(5, 5, 1, 10, "BulletPuff")

        D3CG A 0 A_JumpIfNoAmmo("Down")
        D3CG A 0 A_JumpIfInventory("AltFired", 1, 1)
        goto Down

        D3CG A 0 A_JumpIfInventory("MainFired", 1, "Shoot")
        D3CG A 0 A_ReFire
        goto Down

      Down:
        D3CG A 0 ACS_Execute(876, 0, 1, 0, 0)
        D3CG BA 1 A_ReFire
        D3CG BA 2 A_ReFire
        D3CG A 2 A_PlaySound("jkemod/fmodminigun/down", 6)
        D3CG BA 3 A_ReFire
        D3CG BA 4
        D3CG BA 5
        D3CG BA 6
        goto Ready

      AltFlash:
        D3CF A 0 A_Jump(255, "AltFlash1", "AltFlash2", "AltFlash3", "AltFlash4")

      AltFlash1:
        D3CF A 2 bright A_Light1
        D3CF A 0 A_Light0
        goto LightDone

      AltFlash2:
        D3CF B 2 bright A_Light1
        D3CF A 0 A_Light0
        goto LightDone

      AltFlash3:
        D3CF C 2 bright A_Light1
        D3CF A 0 A_Light0
        goto LightDone

      AltFlash4:
        D3CF D 2 bright A_Light1
        D3CF A 0 A_Light0
        goto LightDone

      Spawn:
        D3CG Z -1
        stop
    }
}


actor " Power L-Minigun ": " L-Minigun "
{
    Weapon.AmmoGive1 0
    
    Weapon.SisterWeapon " L-Minigun "
    +POWERED_UP

    Obituary "$JKE_MP_FMODMINIGUN_POWER"

    States
    {
      AltFire:
        D3CG B 0 A_PlaySound("jkemod/fmodminigun/up", 6)
        D3CG B 6
        D3CG A 5
        D3CG B 4
        D3CG A 3
        D3CG B 2
        D3CG A 1
        D3CG A 0 A_ReFire
        goto Down

      AltHold:
        D3CG A 0 ACS_Execute(876, 0, 1, 0, 0)
        D3CG A 0 A_PlaySound("jkemod/fmodminigun/spin")
        D3CG BABABA 1 A_JumpIfInventory("MainFired", 1, "Shoot")
        D3CG A 0 A_ReFire
        goto Down

      Shoot:
        D3CG A 0 A_AlertMonsters
        D3CG A 0 ACS_ExecuteAlways(876, 0, 0, 20, 0)
        D3CG A 0 Radius_Quake(2, 3, 0, 1, 0)
        
        D3CG A 0 A_GunFlash
        D3CG B 0 ACS_ExecuteAlways(474, 0, 0, 10)
        D3CG A 1 A_FireCustomMissile("LMinigunFire", random(-30, 30)/10.0, 1, 0, 0, 0, random(-30, 30)/10.0)
        
        D3CG B 0 ACS_ExecuteAlways(474, 0, 0, 10)
        D3CG B 1 A_FireCustomMissile("LMinigunFire", random(-30, 30)/10.0, 1, 0, 0, 0, random(-30, 30)/10.0)
        
        D3CG A 0 A_JumpIfNoAmmo("Down")
        D3CG A 0 A_JumpIfInventory("AltFired", 1, 1)
        goto Down

        D3CG A 0 A_JumpIfInventory("MainFired", 1, "Shoot")
        D3CG A 0 A_ReFire
        goto Down

      Down:
        D3CG A 0 ACS_Execute(876, 0, 1, 0, 0)
        D3CG B 1 A_ReFire
        D3CG A 2 A_ReFire
        D3CG A 2 A_PlaySound("jkemod/fmodminigun/down", 6)
        D3CG B 3 A_ReFire
        D3CG A 4
        D3CG B 5
        D3CG A 6
        goto Ready

      AltFlash:
        D3CF A 0 A_Jump(255, "AltFlash1", "AltFlash2", "AltFlash3", "AltFlash4")
        goto AltFlash1

      AltFlash1:
        D3CF A 2 bright A_Light1
        D3CF A 0 A_Light0
        goto LightDone

      AltFlash2:
        D3CF B 2 bright A_Light1
        D3CF A 0 A_Light0
        goto LightDone

      AltFlash3:
        D3CF C 2 bright A_Light1
        D3CF A 0 A_Light0
        goto LightDone

      AltFlash4:
        D3CF D 2 bright A_Light1
        D3CF A 0 A_Light0
        goto LightDone
    }
}

actor LMinigunFire
{
    Radius 14
    Height 8
    Speed 40

    Projectile
    Damage (2)
    +NODAMAGETHRUST
    +FORCERADIUSDMG

    RenderStyle Add
    Scale 0.3

    Decal BulletScorch

    SeeSound "jkemod/jflamethrower/flame"

    Obituary "$JKE_MP_FMODMINIGUN_POWER"

    States
    {
      Spawn:
        SOD0 E 0 bright
        SOD0 E 0 bright A_Explode(16, 64, 0)
        SOD0 E 0 bright ACS_ExecuteAlways(585, 0, 1, random(0, 50))
        SOD0 EF 2 A_Explode(8, 64, 0)
        TNT1 A 0 A_SpawnItemEx("LMinigunFlame", 0,0,0, random(0,4), 0, random(0,3), random(0,359), 0, 128)
        SOD0 GH 2 A_Explode(8, 64, 0)
        TNT1 A 0 A_SpawnItemEx("LMinigunFlame", 0,0,0, random(0,4), 0, random(0,3), random(0,359), 0, 128)
        SOD0 IJ 2 A_Explode(8, 64, 0)
        TNT1 A 0 A_SpawnItemEx("LMinigunFlame", 0,0,0, random(0,4), 0, random(0,3), random(0,359), 0, 128)
        SOD0 KL 2 A_Explode(8, 64, 0)
        TNT1 A 0 A_SpawnItemEx("LMinigunFlame", 0,0,0, random(0,4), 0, random(0,3), random(0,359), 0, 128)
        SOD0 MN 2 A_Explode(8, 64, 0)
        TNT1 A 0 A_SpawnItemEx("LMinigunFlame", 0,0,0, random(0,4), 0, random(0,3), random(0,359), 0, 128)
        SOD0 O 2 A_Explode(8, 64, 0)
        goto NormDeath

      Death:
        TNT1 AAA 0 A_SpawnItemEx("LMinigunFlame", 0,0,0, random(0,4), 0, random(0,3), random(0,359), 0, 128)
        SOD0 ACEGIKMO 1 A_Explode(8, 64, 0)
        stop
      
      NormDeath:
        TNT1 A 0 A_SpawnItemEx("LMinigunFlame", 0,0,0, random(0,4), 0, random(0,3), random(0,359), 0, 128)
        stop
    }
}

actor LMinigunFlame
{
    Speed 5

    Projectile
    -NOGRAVITY
    +NOCLIP
    +RIPPER
    +NOEXPLODEFLOOR
    +NODAMAGETHRUST
    +FORCERADIUSDMG
    +SLIDESONWALLS
    +PUFFGETSOWNER

    RenderStyle Add
    Scale 0.4

    Obituary "$JKE_MP_FMODMINIGUN_POWER"

    States
    {
      Spawn:
        FLME AB 2 bright A_Explode(4, 80, 0)
        FLME A 0 A_ChangeFlag("NOCLIP", 0)
        FLME CDEFGHIJKLMNABCDEFGHIJKLMN 2 bright A_Explode(4, 80, 0)
        goto XDeath

      Crash:
      Death:
        FLME A 0 A_Stop
        goto Spawn

      XDeath:
        FLME A 0 A_Jump(128, "Spawn")
        FLME O 2 bright A_Explode(6, 80, 0)
        FLME P 2 bright A_Explode(4, 80, 0)
        FLME Q 2 bright A_Explode(2, 80, 0)
        stop
    }
}
