
actor " D-1000 ": Weapon
{
    +WEAPON.NOAUTOAIM

    Weapon.AmmoType1 "Cell"
    Weapon.AmmoUse1 10
    Weapon.AmmoGive1 100

    Weapon.SelectionOrder 300

    Inventory.PickupMessage "$JKE_PK_D1000"
    Obituary "$JKE_MP_D1000"
    States
    {
      Ready:
        D1VG A 1 A_WeaponReady
      Select:
        D1VG A 1 A_Raise
        loop
      Deselect:
        D1VG A 1 A_Lower
        loop
      Fire:
        D1VG A 5
        D1VG A 15 A_PlaySound("jkemod/d1000/charge", CHAN_WEAPON)
        D1VG B 4
        D1VG C 2
        D1VG D 2
        D1VG E 1
        D1VG F 1
        D1VG J 2

        D1VG K 0 A_FireCustomMissile("D1000PlasmaBig", 20, 0, 0, -3)
        D1VG K 0 A_FireCustomMissile("D1000Plasma", 20, 0, 0, 0)
        D1VG K 0 A_FireCustomMissile("D1000Plasma", 10, 0, 0, 0)
        D1VG K 0 A_FireCustomMissile("D1000Plasma", 5, 0, 0, 0)
        D1VG K 0 A_FireBullets(0, 0, 1, 0, "D1000Puff")
        D1VG K 0 A_FireCustomMissile("D1000Plasma", -5, 0, 0, 0)
        D1VG K 0 A_FireCustomMissile("D1000Plasma", -10, 0, 0, 0)
        D1VG K 0 A_FireCustomMissile("D1000Plasma", -20, 0, 0, 0)
        D1VG K 0 A_FireCustomMissile("D1000PlasmaBig", -20, 0, 0, -3)
        D1VG K 2 bright

        D1VG L 2 bright
        D1VG D 3 bright A_ReFire
        D1VG C 2 A_PlaySound("jkemod/d1000/cool", CHAN_WEAPON)
        D1VG B 2
        D1VG A 20
        goto Ready

      Hold:
        D1VG J 2 bright A_FireCustomMissile("D1000PlasmaBig", 0, 0, 0, -3)

        D1VG L 0 A_FireCustomMissile("D1000PlasmaBig", 20, 0, 0, -3)
        D1VG K 0 A_FireCustomMissile("D1000Plasma", 20, 0, 0, 0)
        D1VG K 0 A_FireCustomMissile("D1000Plasma", 10, 0, 0, 0)
        D1VG K 0 A_FireCustomMissile("D1000Plasma", 5, 0, 0, 0)
        D1VG K 0 A_FireBullets(0, 0, 1, 0, "D1000Puff")
        D1VG K 0 A_FireCustomMissile("D1000Plasma", -5, 0, 0, 0)
        D1VG K 0 A_FireCustomMissile("D1000Plasma", -10, 0, 0, 0)
        D1VG K 0 A_FireCustomMissile("D1000Plasma", -20, 0, 0, 0)
        D1VG L 0 A_FireCustomMissile("D1000PlasmaBig", -20, 0, 0, -3)
        D1VG L 4 bright
        D1VG D 3 A_ReFire
        D1VG C 2 A_PlaySound("weapons/bfg10kcool", CHAN_WEAPON)
        D1VG B 2
        D1VG A 35
        goto Ready

      Spawn:
        D1VG Z -1
        stop
    }
}

actor D1000Plasma
{
    Radius 6
    Height 5
    Speed 30

    Projectile
    Damage 10

    Translation "112:127=176:191"
    RenderStyle Add
    Alpha 0.75

    DeathSound "jkemod/d1000/plasmaexplode"

    States
    {
      Spawn:
        APLS AB 4 bright
        loop
      Death:
        APBX BCDE 4 bright
        stop
    }
}

actor D1000PlasmaBig
{
    Radius 8
    Height 8
    Speed 20

    Projectile
    Damage 25

    +FORCERADIUSDMG

    Translation "112:127=176:191"
    RenderStyle Add
    Scale 0.4

    DeathSound "jkemod/d1000/plasmabigexplode"

    States
    {
      Spawn:
        BFS1 AB 4 bright
        loop
      Death:
        BFE1 A 0 A_Explode(96, 128, 0)
        BFE1 ABCDEF 6 bright
        stop
    }
}

actor D1000Puff: BulletPuff
{
    +PUFFONACTORS
    +PUFFGETSOWNER
    +FORCERADIUSDMG

    VSpeed 0
    RenderStyle Add

    Translation "112:127=176:191"

    States
    {
      Spawn:
        TNT1 A 0 A_PlaySound("jkemod/d1000/puffexplode")
        TNT1 A 0 A_Explode(192, 128, 0)
        BFE1 ABCDEF 4 bright
        Stop
    }
}