actor " Tri-Staff ": Weapon
{
    Weapon.AmmoType1 "JKEMana"
    Weapon.AmmoType2 "TriStaffCharge"
    Weapon.AmmoUse1 2
    Weapon.AmmoUse2 0
    Weapon.AmmoGive1 100
    Weapon.SelectionOrder 500

    +NOALERT

    Weapon.SisterWeapon " Power Tri-Staff "

    Weapon.UpSound "jkemod/tristaff/up"

    Decal RailScorchLower

    Inventory.PickupMessage "$JKE_PK_TRISTAFF"
    Obituary "$JKE_MP_TRISTAFF"

    States
    {
      Select:
        ST3F A 0 A_TakeInventory("TriStaffCharge")
        ST3F A 1 A_Raise
        Loop

      Deselect:
        ST3F A 1 A_Lower
        Loop

      Ready:
        ST3F A 1 A_WeaponReady
        Loop

      Fire:
        ST3F B 0 A_Light1

        ST3F A 0 A_AlertMonsters
        ST3F C 0 A_PlaySound("jkemod/tristaff/fire", CHAN_WEAPON)
        ST3F C 3 bright A_RailAttack(25,  0, 1, none, "00 00 FF", 1, 0)
        ST3F A 2 bright
        ST3F A 0 A_JumpIfInventory("MainFired", 1, 1)
        goto Ready

        ST3F A 0 A_AlertMonsters
        ST3F B 0 A_PlaySound("jkemod/tristaff/fire", CHAN_WEAPON)
        ST3F B 3 bright A_RailAttack(25, -7, 1, none, "00 FF 00", 1, 0)
        ST3F A 2 bright
        ST3F A 0 A_JumpIfInventory("MainFired", 1, 1)
        goto Ready

        ST3F A 0 A_AlertMonsters
        ST3F C 0 A_PlaySound("jkemod/tristaff/fire", CHAN_WEAPON)
        ST3F D 3 bright A_RailAttack(25,  7, 1, none, "FF 00 00", 1, 0)
        ST3F A 2 bright

        ST3F A 0 A_Light0
        Goto Ready

      AltFire:
        ST3F A 0 A_JumpIfInventory("JKEMana", 3, "AltHold")
        goto NoAlt

      NoAlt:
        ST3F A 1
        goto Ready

      AltHold:
        ST3F A 0 A_JumpIfInventory("TriStaffCharge", ACS_ExecuteWithResult(873, 1, 3), 2)
        ST3F A 0 A_GiveInventory("TriStaffCharge")
        ST3F A 3
        ST3F A 0 A_JumpIfInventory("MainFired", 1, "AltHold2")
        ST3F A 0 A_JumpIfInventory("AltFired", 1, "AltHold")
        goto AltBlast

      AltHold2:
        ST3F A 0 A_JumpIfInventory("TriStaffCharge", 1, "AltUncharge")
        goto AltHold2_2

      AltUncharge:
        ST3F A 0 A_TakeInventory("TriStaffCharge", 1)
        goto AltHold2_2

      AltHold2_2:
        ST3F A 3
        ST3F A 0 A_JumpIfInventory("MainFired", 1, "AltHold2")
        ST3F A 0 A_JumpIfInventory("AltFired", 1, "AltHold")
        goto AltBlast

      AltBlast:
        ST3F A 0 A_JumpIfInventory("TriStaffCharge", 1, 1)
        goto Ready

        ST3F A 0 A_AlertMonsters
        ST3F A 0 A_Light2

        ST3F A 0 A_PlaySound("jkemod/tristaff/fire", 5)
        ST3F A 0 A_PlaySound("jkemod/tristaff/fire", 6)
        ST3F A 0 A_PlaySound("jkemod/tristaff/fire", 7)

        ST3F B 0 A_RailAttack(ACS_ExecuteWithResult(873, 0, 10), -7, 1, none, "00 FF 00", 1, ACS_ExecuteWithResult(873, 0)/10.0)
        ST3F B 0 A_RailAttack(ACS_ExecuteWithResult(873, 0, 10),  0, 1, none, "00 00 FF", 1, ACS_ExecuteWithResult(873, 0)/10.0)
        ST3F B 0 A_RailAttack(ACS_ExecuteWithResult(873, 0, 10),  7, 1, none, "FF 00 00", 1, ACS_ExecuteWithResult(873, 0)/10.0)
        ST3F B 0 A_TakeInventory("JKEMana", ACS_ExecuteWithResult(873, 0, 3))

        ST3F E 8 bright offset(0, 40) A_TakeInventory("TriStaffCharge")
        ST3F E 3 bright offset(0, 38)
        ST3F A 3 bright offset(0, 34) A_Light0
        goto Ready

      Spawn:
        ST3F Z -1
        Stop
    }
}

actor " Power Tri-Staff ": " Tri-Staff "
{
    Weapon.AmmoUse1 2
    Weapon.AmmoUse2 0
    Weapon.AmmoGive1 0

    Weapon.SisterWeapon " Tri-Staff "
    +POWERED_UP

    States
    {
      Fire:
        ST3F B 0 A_Light1

        ST3F C 0 A_PlaySound("jkemod/tristaff/fire", CHAN_WEAPON)
        ST3F C 3 bright A_RailAttack(75,  0, 1, none, "00 00 FF", 1, 0)
        ST3F A 0 A_JumpIfInventory("MainFired", 1, 1)
        goto Ready

        ST3F B 0 A_PlaySound("jkemod/tristaff/fire", CHAN_WEAPON)
        ST3F B 3 bright A_RailAttack(75, -7, 1, none, "00 FF 00", 1, 0)
        ST3F A 0 A_JumpIfInventory("MainFired", 1, 1)
        goto Ready

        ST3F C 0 A_PlaySound("jkemod/tristaff/fire", CHAN_WEAPON)
        ST3F D 3 bright A_RailAttack(75,  7, 1, none, "FF 00 00", 1, 0)

        ST3F A 0 A_Light0
        Goto Ready

      AltHold:
        ST3F A 0 A_JumpIfInventory("TriStaffCharge", ACS_ExecuteWithResult(873, 1, 9), 2)
        ST3F A 0 A_GiveInventory("TriStaffCharge")
        ST3F A 3
        ST3F A 0 A_JumpIfInventory("MainFired", 1, "AltHold2")
        ST3F A 0 A_JumpIfInventory("AltFired", 1, "AltHold")
        goto AltBlast

      AltHold2:
        ST3F A 0 A_JumpIfInventory("TriStaffCharge", 1, "AltUncharge")
        goto AltHold2_2

      AltUncharge:
        ST3F A 0 A_TakeInventory("TriStaffCharge", 1)
        goto AltHold2_2

      AltHold2_2:
        ST3F A 3
        ST3F A 0 A_JumpIfInventory("MainFired", 1, "AltHold2")
        ST3F A 0 A_JumpIfInventory("AltFired", 1, "AltHold")
        goto AltBlast

      AltBlast:
        ST3F A 0 A_JumpIfInventory("TriStaffCharge", 1, 1)
        goto Ready

        ST3F A 0 A_AlertMonsters
        ST3F A 0 A_Light2

        ST3F A 0 A_PlaySound("jkemod/tristaff/fire", 5)
        ST3F A 0 A_PlaySound("jkemod/tristaff/fire", 6)
        ST3F A 0 A_PlaySound("jkemod/tristaff/fire", 7)

        ST3F B 0 A_RailAttack(ACS_ExecuteWithResult(873, 0, 30), -7, 1, none, "00 FF 00", 1, ACS_ExecuteWithResult(873, 0)/10.0)
        ST3F B 0 A_RailAttack(ACS_ExecuteWithResult(873, 0, 30),  0, 1, none, "00 00 FF", 1, ACS_ExecuteWithResult(873, 0)/10.0)
        ST3F B 0 A_RailAttack(ACS_ExecuteWithResult(873, 0, 30),  7, 1, none, "FF 00 00", 1, ACS_ExecuteWithResult(873, 0)/10.0)
        ST3F B 0 A_TakeInventory("JKEMana", ACS_ExecuteWithResult(873, 0, 9))

        ST3F E 8 bright offset(0, 40) A_TakeInventory("TriStaffCharge")
        ST3F E 3 bright offset(0, 38)
        ST3F A 3 bright offset(0, 34) A_Light0
        goto Ready

      Spawn:
        ST3F Z -1
        Stop
    }
}


actor TriStaffCharge: Ammo
{
    Inventory.Icon "JMANA0"
    Inventory.MaxAmount 30
    +UNDROPPABLE
    +IGNORESKILL
}
