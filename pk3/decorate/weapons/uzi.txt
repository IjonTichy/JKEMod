
actor " Uzi ": Weapon
{
    +AMMO_OPTIONAL
    Weapon.AmmoType1 "UziCounter"
    Weapon.AmmoType2 "Clip"
    Weapon.AmmoUse1 1
    Weapon.AmmoUse2 0
    Weapon.AmmoGive1 50
    Weapon.AmmoGive2 50

    Weapon.SelectionOrder 1000
    AttackSound "jkemod/uzi/fire"

    Inventory.PickupMessage "$JKE_PK_UZI"
    Obituary "$JKE_MP_UZI"

    States
    {
      Select:
        UZIC AAAAAAAAAAAAAAA 1 A_Raise
        UZIC BC 3
        UZIC D 0 A_PlaySound("jkemod/uzi/cock", CHAN_WEAPON)
        UZIC DEFBA 3
        goto Ready

      Deselect:
        UZIG AA 0 A_Lower
        UZIG A 1
        loop

      Ready:
        UZIG A 1 A_WeaponReady
        Loop


      Fire:
        UZIG A 0 A_JumpIfNoAmmo("AltFire")

        UZIG B 1 bright A_Light1
        UZIG C 0 A_FireCustomMissile("PistolCasing", -40, 0, 2, 6)
        UZIG C 1 bright A_FireBullets(2, 2, 1, 6, "BulletPuff")
        UZIG D 1 bright A_Light0
        UZIG A 3 A_ReFire
        goto Ready


      AltFire:
        UZIG A 0 A_JumpIfInventory("UziCounter", 0, "NoReload")
        UZIG A 0 A_JumpIfInventory("Clip", 1, "ReloadStart")
        goto NoReload

      ReloadStart:
        UZIR A 4
        UZIR B 0 A_PlaySound("jkemod/uzi/open", CHAN_WEAPON)
        UZIR BCD 3
        UZIR E 25
        UZIR F 3
        goto ReloadLoop

      ReloadLoop:
        UZIC A 0 A_TakeInventory("Clip", 1)
        UZIC A 0 A_GiveInventory("UziCounter", 1)
        UZIC A 0 A_JumpIfInventory("UziCounter", 0, "ReloadEnd")
        UZIC A 0 A_JumpIfInventory("Clip", 1, "ReloadLoop")
        goto ReloadEnd

      ReloadEnd:
        UZIR G 0 A_PlaySound("jkemod/uzi/load", CHAN_WEAPON)
        UZIR GH 3
        UZIG A 3
        UZIC BC 3
        UZIC D 0 A_PlaySound("jkemod/uzi/cock", CHAN_WEAPON)
        UZIC DEFB 3
        UZIC A 2
        goto Ready

      NoReload:
        UZIG A 1
        UZIG A 0 A_ReFire
        goto Ready

      Spawn:
        UZIP A -1
        stop
    }
}

actor UziCounter: Ammo
{
    +IGNORESKILL
    Inventory.MaxAmount 50
}