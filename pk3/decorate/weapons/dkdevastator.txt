
actor DevastatorSide: Inventory
{
    Inventory.MaxAmount 1
    +UNDROPPABLE
}

actor " Devastator Weapon ": Weapon
{
    Inventory.PickupMessage "$JKE_PK_DKDEVASTATOR"
    weapon.AmmoGive 20
    weapon.AmmoType "DevastatorAmmo"
    weapon.AmmoUse1 1
    weapon.selectionorder 500
    Obituary "$JKE_MP_DKDEVASTATOR"
    states
    {
      Select:
        DEVG A 1 A_Raise
        loop
      Deselect:
        DEVG A 1 A_Lower
        loop

      Ready:
        DEVG A 1 A_WeaponReady
        loop

      Fire:
        DEVG A 0 A_JumpIfInventory("DevastatorSide", 1, "FireLeft")
        goto FireRight


      FireLeft:
        DEVG B 0 A_TakeInventory("DevastatorSide")
        DEVG B 0 A_Light1
        DEVG BB 2 bright A_FireCustomMissile("DKDevastatorShot", random(-2, 2), 1, -8, 0)
        DEVG A 2 A_Light0
        DEVG A 0 A_ReFire
        goto Ready

      FireRight:
        DEVG B 0 A_GiveInventory("DevastatorSide")
        DEVG B 0 A_Light1
        DEVG CC 2 bright A_FireCustomMissile("DKDevastatorShot", random(-2, 2), 1,  8, 0)
        DEVG A 2 A_Light0
        DEVG A 0 A_ReFire
        goto Ready


      Spawn:
        DVST A -1
        stop
    }
}


actor DKDevastatorShot
{
    Speed 40
    Radius 6
    Height 5

    Projectile
    +DEHEXPLOSION
    +FORCERADIUSDMG

    Damage 10

    SeeSound "jkemod/dkdevastator/fire"
    DeathSound "jkemod/dkdevastator/explode"

    Scale 0.3

    States
    {
      Spawn:
        MNSS A 1 bright
        Loop
      Death:
        SOD0 E 0 A_Explode(96, 96)
        SOD0 EFGHIJKLMNO 3 bright
        Stop
    }
}