
actor " Flamethrower ": Weapon
{
    Weapon.AmmoType1 "JKEGas"
    Weapon.AmmoUse1 1
    Weapon.AmmoGive1 50
    
    Weapon.SelectionOrder 780
    
    Inventory.PickupMessage "$JKE_PK_JFLAMETHROWER"
    States
    {
      Select:
        FLMG A 1 A_Raise
        loop
        
      Deselect:
        FLMG A 0 A_StopSoundEx("Weapon")
        FLMG A 1 A_Lower
        loop
        
      Ready:
        FLMG A 0 A_StopSoundEx("Weapon")
        FLMG BCD 2 A_WeaponReady
        Loop
      Fire:
        FLMG E 1 bright
        FLMG E 0 A_PlaySoundEx("jkemod/jflamethrower/fire", "Weapon", 1)
        goto FireLoop
    

      FireLoop:  
        FLMG A 0 A_JumpIfInventory("FlameLoop", 0, "FireEnd")
        FLMG A 0 A_GiveInventory("FlameLoop")
        
        FLMG A 0 A_Jump(64, "AltProjectile")
        FLMG F 1 bright A_FireCustomMissile("FlamethrowerShot1", 0, 1, 0, 0)
        goto FireLoop

      AltProjectile:
        FLMG F 1 bright A_FireCustomMissile("FlamethrowerShot2", 0, 1, 0, 0)
        goto FireLoop
        

      FireEnd:
        FLMG A 0 A_TakeInventory("FlameLoop")
        FLMG G 2 bright A_ReFire
        FLMG H 3 bright
        FLMG E 0 A_StopSoundEx("jkemod/jflamethrower/fire")
        FLMG H 0 bright A_PlaySound("jkemod/jflamethrower/down", CHAN_WEAPON)
        FLMG A 35
        goto Ready
        
      Spawn:
        FLMG Z -1
        stop
    }
}


actor FlameLoop: Inventory
{
    Inventory.MaxAmount 2
    +UNDROPPABLE
}

actor FlamethrowerShot
{
    Radius 10
    Height 8
    Speed 22
    
    Projectile
    +RIPPER
    +NODAMAGETHRUST
    
    RenderStyle Add
    Scale 0.3
    
    Obituary "$JKE_MP_JFLAMETHROWER_SHOT"
}


actor FlamethrowerShot1: FlamethrowerShot
{
    States
    {
      Spawn:
        SOD0 EFGHIJKLMNO 2 A_Explode(4, 64, 0)
        loop

      Death:
        TNT1 A 0
        stop
    }
}

actor FlamethrowerShot2: FlamethrowerShot
{
    States
    {
      Spawn:
        SOD0 EFGHIJKLMNO 2 A_Explode(4, 64, 0)
        TNT1 A 0 A_SpawnItemEx("FlamethrowerFlame", 0, 0, 0, random(-1, 1), random(-1, 1) )
        loop

      Death:
        TNT1 A 0 A_SpawnItemEx("FlamethrowerFlame", 0, 0, 0, random(-1, 1), random(-1, 1) )
        stop
    }
}

actor FlamethrowerFlame
{
    Speed 5
    
    Projectile
    -NOGRAVITY
    +NOCLIP
    +RIPPER
    +FORCERADIUSDMG
    +NOEXPLODEFLOOR
    +NODAMAGETHRUST
    +SLIDESONWALLS
    +PUFFGETSOWNER
    
    RenderStyle Add

    SeeSound "jkemod/jflamethrower/flame"
    
    Obituary "$JKE_MP_JFLAMETHROWER_FLAME"
    
    States
    {
      Spawn:
        FLME AB 2 bright A_Explode(4,64)
        FLME A 0 A_ChangeFlag("NOCLIP", 0)
        FLME CDEFGHIJKLMNABCDEFGHIJKLMN 2 bright A_Explode(4,32)
        goto XDeath

      Crash:
      Death:
        FLME A 0 A_Stop
        goto Spawn
      XDeath:
        FLME O 2 bright A_Explode(3,64)
        FLME P 2 bright A_Explode(2,64)
        FLME Q 2 bright A_Explode(1,64)
        stop
    }
}
